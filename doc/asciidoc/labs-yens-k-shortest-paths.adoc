[[labs-algorithms-yens-k-shortest-path]]
= The Yen's K-shortest paths algorithm

[abstract]
--
This section describes the Yen's K-shortest paths algorithm in the Neo4j Labs Graph Algorithms library.
--

Yen's K-shortest paths algorithm computes single-source K-shortest loopless paths for a graph with non-negative relationship weights.

[WARNING]
--
The Yen's K-shortest paths algorithm was developed by the Neo4j Labs team and is not officially supported.
--

This section includes:

* <<algorithms-yens-k-shortest-path-context, History and explanation>>
* <<algorithms-yens-k-shortest-path-usecase, Use-cases - when to use the Yen's K-shortest paths algorithm>>
* <<algorithms-yens-k-shortest-path-limitations, Constraints - when not to use the Yen's K-shortest paths algorithm>>
* <<algorithms-yens-k-shortest-path-syntax, Syntax>>
* <<algorithms-yens-k-shortest-path-sample, Yen's K-shortest paths algorithm sample>>
* <<algorithms-yens-k-shortest-path-cp, Cypher projection>>


[[algorithms-yens-k-shortest-path-context]]
== History and explanation

// tag::explanation[]
Algorithm was defined in 1971 by Jin Y. Yen in the research paper https://pubsonline.informs.org/doi/abs/10.1287/mnsc.17.11.712[Finding the K Shortest Loopless Paths in a Network].
Our implementation uses Dijkstra algorithm to find the shortest path and then proceeds to find k-1 deviations of the shortest paths.
// end::explanation[]


[[algorithms-yens-k-shortest-path-usecase]]
== Use-cases - when to use the Yen's K-shortest paths algorithm

// tag::use-case[]
* K-shortest paths algorithm has been used to optimize multiple object tracking by formalizing the motions of targets as flows along the relationships of the spatial graph.
  Find more in https://cvlab.epfl.ch/files/content/sites/cvlab2/files/publications/publications/2011/BerclazFTF11.pdf[Multiple Object Tracking using K-Shortest Paths Optimization]
* K-shortest paths algorithm is used to study alternative routing on road networks and to recommend top k-paths to the user.
  Find this study in https://www.informatik.hu-berlin.de/de/forschung/gebiete/wbi/research/publications/2015/sigspatial_kshortest.pdf[Alternative Routing: k-Shortest Paths with Limited Overlap]
* K-shortest paths algorithm has been used as part of https://pdfs.semanticscholar.org/51e7/8aa2041d595b3871a49f4b92be725199e73d.pdf[Finding Diverse High-Quality Plans for Hypothesis Generation] process.
// end::use-case[]


[[algorithms-yens-k-shortest-path-limitations]]
== Constraints - when not to use the Yen's K-shortest paths algorithm

// tag::constraint[]
Yen's K-Shortest paths algorithm does not support negative weights.
The algorithm assumes that adding a relationship to a path can never make a path shorter - an invariant that would be violated with negative weights.
// end::constraint[]


[[algorithms-yens-k-shortest-path-syntax]]
== Syntax

.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.alpha.kShortestPaths.write(configuration: MAP)
YIELD resultCount, loadMillis, evalMillis, writeMillis

----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name                | Type    | Default     | Optional | Description
| startNode           | node    | null        | no       | The start node
| endNode             | node    | null        | no       | The end node
| weightProperty      | string  | null        | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| nodeQuery           | string  | null        | yes      | The label to load from the graph. If null, load all nodes
| relationshipQuery   | string  | null        | yes      | The relationship type to load from the graph. If null, load all nodes
| direction           | string  | both        | yes      | The relationship direction to load from the graph. If 'both', treats the relationships as undirected
| defaultValue        | float   | null        | yes      | The default value of the weight in case it is missing or invalid
| maxDepth            | int     | Integer.MAX | yes      | The depth of the shortest paths traversal
| write               | boolean | true        | yes      | Specifies if the result should be written back as a node property
| writePropertyPrefix | string  | 'PATH_'     | yes      | The relationship-type prefix written back to the graph
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name        | Type | Description
| resultCount | int  | The number of shortest paths results
| loadMillis  | int  | Milliseconds for loading data
| evalMillis  | int  | Milliseconds for running the algorithm
| writeMillis | int  | Milliseconds for writing result data back
|===


[[algorithms-yens-k-shortest-path-sample]]
== Yen's K-shortest paths algorithm sample

image::yens.png[]

.The following will create a sample graph:
[source, cypher]
----
CREATE (a:Loc {name:'A'}),
      (b:Loc {name:'B'}),
      (c:Loc {name:'C'}),
      (d:Loc {name:'D'}),
      (e:Loc {name:'E'}),
      (f:Loc {name:'F'}),
      (a)-[:ROAD {cost:50}]->(b),
      (a)-[:ROAD {cost:50}]->(c),
      (a)-[:ROAD {cost:100}]->(d),
      (b)-[:ROAD {cost:40}]->(d),
      (c)-[:ROAD {cost:40}]->(d),
      (c)-[:ROAD {cost:80}]->(e),
      (d)-[:ROAD {cost:30}]->(e),
      (d)-[:ROAD {cost:80}]->(f),
      (e)-[:ROAD {cost:40}]->(f);
----

.The following will run the algorithm and stream results:
[source, cypher]
----
MATCH (start:Loc{name:'A'}), (end:Loc{name:'F'})
CALL gds.alpha.kShortestPaths.stream({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost'
    }
  },
  startNode: start,
  endNode: end,
  k: 3,
  weightProperty: 'cost'
})
YIELD index, nodeIds, costs
RETURN [node in algo.getNodesById(nodeIds) | node.name] AS places,
       costs,
       reduce(acc = 0.0, cost in costs | acc + cost) AS totalCost
----

.Results
[opts="header"]
|===
| `places`                    | `costs`                    | `totalCost`
| ["A", "B", "D", "E", "F"] | [50.0, 40.0, 30.0, 40.0] | 160.0
| ["A", "C", "D", "E", "F"] | [50.0, 40.0, 30.0, 40.0] | 160.0
| ["A", "B", "D", "F"]      | [50.0, 40.0, 80.0]       | 170.0
|===

This procedure doesn't return paths by default, but we can have it return those by passing in the config `path: true`.

.The following will run the algorithm and stream results, including paths:
[source, cypher]
----
MATCH (start:Loc{name:'A'}), (end:Loc{name:'F'})
CALL gds.alpha.kShortestPaths.stream({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost'
    }
  },
  startNode: start,
  endNode: end,
  k: 3,
  weightProperty: 'cost',
  path: true
})
YIELD path
RETURN path
LIMIT 1
----

image::yens-path.svg[]

.The following will run the algorithm and write back results:
[source, cypher]
----
MATCH (start:Loc{name:'A'}), (end:Loc{name:'F'})
CALL gds.alpha.kShortestPaths.write({
  nodeProjection: 'Loc',
  relationshipProjection: {
    ROAD: {
      type: 'ROAD',
      properties: 'cost'
    }
  },
  startNode: start,
  endNode: end,
  k: 3,
  weightProperty: 'cost'
})
YIELD resultCount
RETURN resultCount
----

.Results
[opts="header"]
|===
| `resultCount`
| 3
|===

.The following will return all 3 of the shortest path:
[source, cypher]
----
MATCH p=()-[r:PATH_0|:PATH_1|:PATH_2]->() RETURN p LIMIT 25
----

image::yens_result.png[]

The quickest route takes us from A to B, via D and E and is saved as `PATH_0`.
Second quickest path is saved as `PATH_1` and third one is saved as`PATH_2`


[[algorithms-yens-k-shortest-path-cp]]
== Cypher projection

include::projected-graph-model/cypher-projection.adoc[tag=explanation]

[source, cypher]
----
MATCH (start:Loc{name:'A'}), (end:Loc{name:'F'})
CALL gds.alpha.kShortestPaths.write({
  nodeQuery:'MATCH(n:Loc) WHERE not n.name = "C" RETURN id(n) as id',
  relationshipQuery:'MATCH (n:Loc)-[r:ROAD]->(m:Loc) RETURN id(n) as source, id(m) as target, r.cost as weight',
  startNode: start,
  endNode: end,
  k: 3,
  weightProperty: 'cost',
  writePropertyPrefix:'cypher_'
})
YIELD resultCount
RETURN resultCount
----
