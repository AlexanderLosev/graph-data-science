// tag::overview[]
[[cypher-projection]]
= Cypher projection

ifdef::env-docs[]
[abstract]
--
This chapter explains how to create a graph using a Cypher projection.
--
endif::env-docs[]

If the store projection is not expressive enough to describe the in-memory graph, we can instead use Cypher queries to select nodes and relationships.
One benefit of using Cypher queries is the possibility to form the graph from data that exists only during query time.
A common use case is the reduction of paths into single relationships between the start and end node of the path.

.The following query reduces a 2-hop pattern to a single relationship effectively representing co-authors:
[source,cypher]
----
MATCH (p1:Author)-[:WROTE]->(a:Article)<-[:WROTE]-(p2:Author)
RETURN id(p1) AS source, id(p2) AS target, count(a) AS weight
----

Cypher projections are especially useful during the development phase, when the workflow is being designed and the inputs are not yet clear.
However, despite being more expressive, a Cypher projection usually increases loading time significantly.
For the production phase, one might consider adapting the domain model to be eligible for the store projection.

[[cypher-projection-syntax]]
== Syntax

A Cypher projection takes three mandatory arguments: `graphName`, `nodeQuery` and `relationshipQuery`.

[source,cypher]
----
CALL gds.graph.create.cypher(
    graphName: string,
    nodeQuery: string,
    relationshipQuery: string,
    configuration: map
)
----

.Parameters
[opts="header",cols="1m,1m,1m"]
|===
| Name              | Optional | Description
| graphName         | no       | The name under which the graph is stored in the catalog.
| nodeQuery         | no       | Cypher query to project nodes.
| relationshipQuery | no       | Cypher query to project relationships.
| configuration     | yes      | Additional parameters to configure the Cypher projection.
|===

.Configuration
[opts="header",cols="1,1,1,4"]
|===
| Name                   | Type    | Default        | Description
| nodeProperties         | map     | empty map      | Mappings between the RETURN items and node properties in the graph projection.
| relationshipProperties | map     | empty map      | Mappings between the RETURN items and relationship properties in the graph projection.
| concurrency            | int     | available CPUs | The number of concurrent threads used for creating the graph.
|===

[[cypher-projection-query-constraints]]
== Query constraints

The node query projects nodes and optionally their properties to an in-memory graph.
Each row in the query result represents a node in the projected graph.

The query result must contain a column called `id`.
The value in that column is used to uniquely identify the node.

.Minimal example of a node query used for Cypher projection.
[source,cypher]
----
MATCH (n) RETURN id(n) AS id
----

The relationship query projects relationships and optionally their type and properties to an in-memory graph.
Each row in the query result represents a relationship in the projected graph.

The query result must contain a column called `source` and a column called `target`.
The values in those columns represent the source node id and the target node id of the relationship.
The values are used to connect the relationships to the nodes selected by the node query.
If either the source or the target value can not be mapped to a node, the relationship is not projected.

.Minimal example of a relationship query used for Cypher projection.
[source,cypher]
----
MATCH (n)-->(m) RETURN id(n) AS source, id(m) AS target
----

Using both example queries in a Cypher projection, we can project the whole Neo4j database into an in-memory graph and store it in the catalog:

[source,cypher]
----
CALL gds.graph.create.cypher(
    'myCypherGraph',
    'MATCH (n) RETURN id(n) AS id',
    'MATCH (n)-->(m) RETURN id(n) AS source, id(m) AS target'
)
----

[[cypher-projection-properties]]
== Node and relationship properties

Similar to the default store projection, we can load node and relationship properties using a Cypher projection.

Both, node and relationship queries, expect mandatory columns to be returned, i.e., `id`, `source` and `target`.
If a query returns additional columns, we use those columns as node and relationship properties, respectively.

The values stored in property columns need to be numeric.
If a value is `null`, we load a default value (`Double.NaN`) instead.
If we want to use a different default value, the `coalesce` function can be used.

The following Cypher projections loads multiple node and relationship properties:

.Projecting query columns into node and relationship properties.
[source,cypher]
----
CALL gds.graph.create.cypher(
    'myCypherGraph',
    'MATCH (n:City) RETURN id(n) AS id, n.zipCode AS community, n.population AS population',
    'MATCH (n:City)-[r:ROAD]->(m:City) RETURN id(n) AS source, id(m) AS target, r.distance AS distance, coalesce(r.condition, 0.5) AS quality'
)
----

The projected properties can be referred to by any algorithm that uses properties as input, for example, Label Propagation.

[source,cypher]
----
CALL gds.labelPropgation.stream(
    'myCypherGraph', {
        seedProperty: 'community',
        relationshipWeightProperty: 'quality'
    }
)
----

[[cypher-projection-relationship-types]]
== Relationship types

The store graph projection supports loading multiple relationship types which can be filtered in each algorithm execution.
The Cypher projection can achieve the same feature by returning the relationship type in the query.
If the `type` column is present in the query result, we use the values in that column to distinguish relationship types.

For the following example, let's assume `City` nodes to be connected by either `ROAD` or `RAIL` relationships.

.Using the `type` column to distinguish between multiple relationship types.
[source,cypher]
----
CALL gds.graph.create.cypher(
    'myCypherGraph',
    'MATCH (n:City) RETURN id(n) AS id',
    'MATCH (n:City)-[r]->(m:City) RETURN id(n) AS source, id(m) AS target, type(r) AS type'
)
----

The loaded graph will be composed of the two relationship types.
This allows us to apply a relationship filter during algorithm execution:

.Using a relationship filter to run the algorithm on a subgraph.
[source,cypher]
----
CALL gds.labelPropgation.stream(
    'myCypherGraph', {
        relationshipFilter: 'ROAD'
    }
)
----


[[cypher-projection-relationship-aggregation]]
== Relationship aggregation

The property graph model supports parallel relationships, which means two nodes can be connected by multiple relationships of the same relationship type.
For some algorithms, we want the projected graph to contain at most one relationship between two nodes.

The simplest way to achieve this, is using a `DISTINCT` clause in the relationship query:

[source,cypher]
----
MATCH (n:City)-[r:ROAD]->(m:City)
RETURN DISINCT id(n) AS source, id(m) AS target
----

If we also want to load relationship properties, aggregating the values of parallel edges can also be achieved using Cypher.

[source,cypher]
----
MATCH (n:City)-[r:ROAD]->(m:City)
RETURN
    id(n) AS source,
    id(m) AS target,
    min(r.distance) AS minDistance,
    max(coalesce(r.condition, 0.5), 1.0) AS maxQuality
----

One drawback of that approach is that we put more pressure on the Cypher execution engine and the query result consumes additional memory.
An alternative approach is to use `relationshipProperties` as part of the optional configuration map.
The syntax is identical to the property mappings used in the store projection.

[source,cypher]
----
CALL gds.graph.create.cypher(
    'myCypherGraph',
    'MATCH (n:City) RETURN id(n) AS id, n.zipCode AS community, n.population AS population',
    'MATCH (n:City)-[r:ROAD]->(m:City) RETURN id(n) AS source, id(m) AS target, r.distance AS distance, r.condition AS quality',
    {
        relationshipProperties: {
            minDistance: {
                property: 'distance',
                aggregation: 'MIN',
                defaultValue: 42.0
            },
            maxQuality: {
                property: 'quality',
                aggregation: 'MAX',
                defaultValue: 1.0
            }
        }
    }
)
----

* The key of each mapping is the name under which the resulting property is stored in the graph.
* The `property` entry refers to the column name in the query result.
* The `aggregation` entry sets the aggregation function for values of parallel relationships (e.g. `MIN` or `MAX`).
* The `defaultValue` entry is used if the cell contains `null` (instead of `Double.NAN`).