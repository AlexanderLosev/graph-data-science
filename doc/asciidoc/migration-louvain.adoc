[[migration-louvain]]
= Louvain

.Changes in Configuration
[opts=header,cols="1a,1a"]
|===
|GA                                 | GDS
| direction                         | -
| levels                            | maxLevels
| concurrency                       | concurrency
| readConcurrency                   | readConcurrency footnote:louvain-read[Only when using anonymous graph]
| writeConcurrency                  | writeConcurrency footnote:louvain-write[Only for `write` mode]
| weightProperty                    | relationshipWeightProperty
| seedProperty                      | seedProperty
| innerIterations                   | maxIterations
| includeIntermediateCommunities    | includeIntermediateCommunities
| tolerance                         | tolerance
| writeProperty                     | writeProperty footnote:louvain-write[]
| write                             | -
| graph                             | -
|===

.Changes in YIELD fields
[opts=header,cols="1a,1a"]
|===
| GA                     | GDS
| loadMillis             | createMillis
| computeMillis          | computeMillis
| writeMillis            | writeMillis
| postProcessingMillis   | postProcessingMillis
| nodes                  | nodePropertiesWritten
| communityCount         | communityCount
| levels                 | ranLevels
| nodeId                 | nodeId footnote:louvain-yield-stream[Only for `stream` mode]
| community              | communityId footnote:louvain-yield-stream[]
| communities            | communityIds footnote:louvain-yield-stream[]
| modularity             | modularity footnote:louvain-yield-write[Only for `write` mode]
| modularities           | modularities footnote:louvain-yield-write[]
| write                  | -
| -                      | communityDistribution
| -                      | configuration footnote:louvain-gds-config[The configuration used to run the algorithm]
| includeIntermediateCommunities footnote:louvain-config[Inlined into `configuration`]         | -
| writeProperty footnote:louvain-config[]         | -
| weightProperty footnote:louvain-weight-config[Inlined into `configuration` as `relationshipWeightProperty`]        | -
| min, max, mean, p50, p75, p90, p95, p99, p999 footnote:louvain-distribution[Inlined into `communityDistribution`] | -
|===

.Louvain Stream Mode
[opts=header,cols="1a,1a"]
|===
|GA | GDS
2+|Minimalistic streaming over named graph
|
[source, cypher]
----
CALL algo.beta.louvain.stream(null, null, {graph: 'myGraph'})
YIELD nodeId, community, communities
----
|
[source, cypher]
----
CALL gds.louvain.stream('myGraph')
YIELD nodeId, communityId, communityIds
----
2+|Streaming over named graph using additional properties - `maxLevels` and `maxIterations`
|
[source, cypher]
----
CALL algo.beta.louvain.stream(
  null,
  null,
  {
    graph: 'myGraph',
    levels: 15,
    innerIterations: 30
  }
)
----
|
[source, cypher]
----
CALL gds.louvain.stream(
  'myGraph',
  {
    maxLevels: 15,
    maxIterations: 30
  }
)
----
2+| Streaming over named graph with weight property
|
[source, cypher]
----
CALL algo.beta.louvain.stream(
  null,
  null,
  {
    graph: 'myGraph',
    weightProperty: 'myWeightProperty'
  }
)
----
|
[source, cypher]
----
CALL gds.louvain.stream(
  'myGraph',
  {
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
2+| Minimalistic streaming over anonymous graph
|
[source, cypher]
----
CALL algo.beta.louvain.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
|
[source, cypher]
----
CALL gds.louvain.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: 'MY_RELATIONSHIP_TYPE'
})
----
2+| Streaming over anonymous graph with `REVERSE` relationship projection
|
[source, cypher]
----
CALL algo.beta.louvain.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'INCOMING' }
)
----
|
[source, cypher]
----
CALL gds.louvain.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'REVERSE'
    }
  }
})
----
2+| Streaming over anonymous graph using two way relationships footnote:[Not algorithm specific. This is also possible with named graphs. The graph has to be created with two relationship projections - one for each direction]
|
[source, cypher]
----
CALL algo.louvain.stream(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  { direction: 'BOTH' }
)
----
|
[source, cypher]
----
CALL gds.louvain.stream({
  nodeProjection: 'MyLabel',
  relationshipProjection: {
    MY_RELATIONSHIP_TYPE_NATURAL: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'NATURAL'
    },
    MY_RELATIONSHIP_TYPE_REVERSE: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'REVERSE'
    }
  }
})
----
|===

.Louvain Write Mode
[opts=header,cols="1a,1a"]
|===
|GA | GDS
2+| Minimalistic write with just `writeProperty`
|
[source, cypher]
----
CALL algo.beta.louvain(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    write: true
  }
)
YIELD
  nodes,
  writeMillis,
  levels,
  iterations,
  p1,
  writeProperty
----
|
[source, cypher]
----
CALL gds.louvain.write(
  'myGraph',
  { writeProperty: 'myWriteProperty' }
)
YIELD
  nodePropertiesWritten,
  writeMillis,
  ranLevels,
  ranIterations,
  communityDistribution AS cd,
  configuration AS conf
RETURN
  nodePropertiesWritten,
  writeMillis,
  ranLevels,
  ranIterations,
  cd.p1 AS p1,
  conf.writeProperty AS writeProperty
----
2+| Running in `write` mode over weighted graph
|
[source, cypher]
----
CALL algo.beta.louvain(
  null,
  null,
  {
    graph: 'myGraph',
    writeProperty: 'myWriteProperty',
    weightProperty: 'myWeightProperty',
    write: true
  }
)
----
|
[source, cypher]
----
CALL gds.louvain.write(
  'myGraph',
  {
    writeProperty: 'myWriteProperty',
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
2+| Memory estimation of the algorithm
|
[source, cypher]
----
CALL algo.memrec(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  'beta.louvain',
  {
    writeProperty: 'myWriteProperty',
    weightProperty: 'myRelationshipWeightProperty',
    write: true
  }
)
----
|
[source, cypher]
----
CALL gds.louvain.write.estimate(
  {
    nodeProjection: 'MyLabel',
    relationshipProjection: 'MY_RELATIONSHIP_TYPE'
  },
  {
    writeProperty: 'myWriteProperty',
    relationshipWeightProperty: 'myWeightProperty'
  }
)
----
|===