[[algorithms-wcc]]
= The Weakly Connected Components algorithm

[abstract]
--
This section describes the Weakly Connected Components (WCC) algorithm in the Neo4j Graph Algorithms library.
--

This section includes:

* <<algorithms-wcc-intro, Introduction>>
* <<algorithms-wcc-syntax, Syntax>>
* <<algorithms-wcc-examples, Examples>>
** <<algorithms-wcc-examples-unweighted, Unweighted>>
** <<algorithms-wcc-examples-weighted, Weighted>>
** <<algorithms-wcc-examples-seeding, Seeded components>>
* <<algorithms-wcc-usage-details, Usage Details>>

* <<algorithms-wcc-hgp, Huge graph projection>>
* <<algorithms-wcc-cp, Cypher projection>>
* <<algorithms-wcc-imp, Implementations>>
* <<algorithms-unionfind-memory-requirements, Memory Requirements>>


[[algorithms-wcc-intro]]
== Introduction

The WCC algorithm finds sets of connected nodes in an undirected graph, where all nodes in the same set form a connected component.
// Don't call out to Labs; let Labs call to us
// It differs from the Strongly Connected Components algorithm (SCC) because it only needs a path to exist between pairs of nodes in one direction, whereas SCC needs a path to exist in both directions.
WCC is often used early in an analysis to understand the structure of a graph.

WCC was previously known as Union Find.
Currently, the WCC algorithm still uses the syntax of `algo.unionFind`.

WCC can be used to keep track of clusters of database records, as part of the de-duplication process - an important task in master data management applications.
WCC can also be used to analyse citation networks.

For more information on this algorithm, see:

* http://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.28.8405["An efficient domain-independent algorithm for detecting approximately duplicate database records"^].
* One study uses WCC to work out how well connected the network is, and then to see whether the connectivity remains if 'hub' or 'authority' nodes are moved from the graph: https://link.springer.com/article/10.1007%2Fs10115-003-0128-3["Characterizing and Mining Citation Graph of Computer Science Literature"^]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-requirements>>.
====


[[algorithms-wcc-syntax]]
== Syntax

.The following will load a graph, run the algorithm, and write back results:
[source, cypher]
----
CALL algo.unionFind(label: String, relationship: String, {
  threshold: 0.42,
  defaultValue: 1.0,
  write: true,
  writeProperty: 'partition',
  weightProperty: 'weight',
  concurrency: 4
})
YIELD nodes, setCount, loadMillis, computeMillis, writeMillis
----

.Parameters
[opts="header",cols="1,1,1m,1,4"]
|===
| Name         | Type    | Default | Optional | Description
| node label   | string  | null    | yes      | The node label to load from the graph. If null, load all nodes.
| relationship | string  | null    | yes      | The relationship type to load from the graph. If null, load all relationships.
| config       | map     | {}      | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name              | Type    | Default                   | Optional | Description
| concurrency       | int     | available CPUs            | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency   | int     | value of 'concurrency'    | yes      | The number of concurrent threads used for reading the graph.
| writeConcurrency  | int     | value of 'concurrency'    | yes      | The number of concurrent threads used for writing the result.
| weightProperty    | string  | `null`                    | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| seedProperty      | string  | n/a                       | yes      | Used to set the initial community for a node. The property value needs to be a number.
| write             | boolean | `true`                    | yes      | Specifies if the result should be written back as a node property.
| writeProperty     | string  | `'partition'`             | yes      | The property name written back the ID of the partition particular node belongs to.
| threshold         | float   | `null`                    | yes      | The value of the weight above which the relationship is not thrown away.
| defaultValue      | float   | `null`                    | yes      | The default value of the weight in case it is missing or invalid.
| consecutiveIds    | boolean | `false`                   | yes      | Community identifiers are mapped into a consecutive id space (requires additional memory).
| graph             | string  | `'huge'`                  | yes      | Use `'huge'` when describing the subset of the graph with label and relationship type parameter. Use `'cypher'` for describing the subset using a Cypher query for nodes and relationships.
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name          | Type | Description
| loadMillis    | int  | Milliseconds for loading data.
| computeMillis | int  | Milliseconds for running the algorithm.
| writeMillis   | int  | Milliseconds for writing result data back.

| postProcessingMillis    | int  | Milliseconds for computing percentiles and community count.
| nodes | int | The number of nodes considered.
| communityCount | int  | The number of communities found.

| p1                   | double  | The 1 percentile of community size.
| p5                   | double  | The 5 percentile of community size.
| p10                   | double  | The 10 percentile of community size.
| p25                   | double  | The 25 percentile of community size.
| p50                   | double  | The 50 percentile of community size.
| p75                   | double  | The 75 percentile of community size.
| p90                   | double  | The 90 percentile of community size.
| p95                   | double  | The 95 percentile of community size.
| p99                   | double  | The 99 percentile of community size.
| p100                  | double  | The 100 percentile of community size.

| write | boolean | Specifies if the result was written back as a node property.
| writeProperty | string | The property name written back to.
|===


.The following will run the algorithm and stream results:
[source, cypher]
----
CALL algo.unionFind.stream(label: String, relationship: String, {
    weightProperty: 'weight',
    threshold: 0.42,
    defaultValue: 1.0,
    concurrency: 4
})
YIELD nodeId, setId
----

.Parameters
[opts="header",cols="1,1,1,1,4"]
|===
| Name              | Type    | Default        | Optional | Description
| node label        | string  | `null`         | yes      | The node label to load from the graph. If null, load all nodes.
| relationship type | string  | `null`         | yes      | The relationship type to load from the graph. If null, load all relationships.
| config            | map     | `{}`           | yes      | Additional configuration, see below.
|===

.Configuration
[opts="header",cols="1m,1,1,1,4"]
|===
| Name              | Type    | Default                   | Optional | Description
| concurrency       | int     | available CPUs            | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency'.
| readConcurrency   | int     | value of 'concurrency'    | yes      | The number of concurrent threads used for reading the graph.
| weightProperty    | string  | `null`                    | yes      | The property name that contains weight. If null, treats the graph as unweighted. Must be numeric.
| seedProperty      | string  | n/a                       | yes      | Used to set the initial community for a node. The property value needs to be a number.
| threshold         | float   | `null`                    | yes      | The value of the weight above which the relationship is not thrown away.
| defaultValue      | float   | `null`                    | yes      | The default value of the weight in case it is missing or invalid.
| consecutiveIds    | boolean | `false`                   | yes      | Community identifiers are mapped into a consecutive id space (requires additional memory).
| graph             | string  | `'huge'`                  | yes      | Use `'huge'` when describing the subset of the graph with label and relationship-type parameter. Use `'cypher'` for describing the subset using a Cypher query for nodes and relationships.
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name   | Type | Description
| nodeId | int  | Node ID
| setId  | int  | Partition ID
|===


[[algorithms-wcc-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE (nAlice:User {name: 'Alice'})
CREATE (nBridget:User {name: 'Bridget'})
CREATE (nCharles:User {name: 'Charles'})
CREATE (nDoug:User {name: 'Doug'})
CREATE (nMark:User {name: 'Mark'})
CREATE (nMichael:User {name: 'Michael'})

CREATE (nAlice)-[:LINK {weight: 0.5}]->(nBridget)
CREATE (nAlice)-[:LINK {weight: 4}]->(nCharles)
CREATE (nMark)-[:LINK {weight: 1.1}]->(nDoug)
CREATE (nMark)-[:LINK {weight: 2}]->(nMichael);
----

This graph has two connected components, each with three nodes.
The relationships that connect the nodes in each component has a property which determines the strength of the relationship.
We load this graph under the name `'wcc-graph'` like so:

[source, cypher]
----
CALL algo.graph.load('wcc-graph', 'User', 'LINK')
YIELD name AS Name, nodes AS Nodes, relationships AS Relationships
----
// TODO: verify these results now that Heavy is removed:

.Results
[opts="header",cols="1m,1m,1m"]
|===
| Name        | Nodes | Relationships
| "wcc-graph" | 6     | 4
|===


The following examples demonstrate using the Connected Components algorithm.


[[algorithms-wcc-examples-unweighted]]
=== Unweighted

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL algo.unionFind.stream('User', 'LINK', {
  graph: 'wcc-graph'
})
YIELD nodeId, setId
RETURN algo.asNode(nodeId).name AS Name, setId AS ComponentId
ORDER BY ComponentId
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | ComponentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 0
| "Michael" | 3
| "Doug"    | 3
| "Mark"    | 3
|===

To instead write the component ID to a node property in the Neo4j graph, use this query:

.The following will run the algorithm and write back results:
[source, cypher]
----
CALL algo.unionFind('User', 'LINK', {
  graph: 'wcc-graph',
  write: true,
  writeProperty: 'componentId'
})
YIELD nodes AS Nodes, setCount AS NbrOfComponents, writeProperty AS PropertyName;
----

.Results
[opts="header",cols="1m,1m,1m"]
|===
| Nodes | NbrOfComponents | PropertyName
| 6     | 2               | "componentId"
|===

As we can see from the results, the nodes connected to one another are calculated by the algorithm as belonging to the same connected component.


[[algorithms-wcc-examples-weighted]]
=== Weighted

By configuring the algorithm to use a weight (`weightProperty`) and a threshold, we can increase granularity in the way the algorithm calculates component assignment.
Only relationships with a weight strictly greater than the threshold value will be considered as a valid connection.

.The following will run the algorithm and stream results:
[source, cypher]
----
CALL algo.unionFind.stream('User', 'LINK', {
  graph: 'wcc-graph',
  weightProperty: 'weight',
  threshold: 1.0
})
YIELD nodeId,setId

RETURN algo.asNode(nodeId).name AS Name, setId AS ComponentId
----

.Results
[opts="header",cols="1m,1m"]
|===
| Name      | ComponentId
| "Alice"   | 0
| "Charles" | 0
| "Bridget" | 1
| "Doug"    | 3
| "Mark"    | 3
| "Michael" | 3
|===


.The following will run the algorithm and write back results:
[source, cypher]
----
CALL algo.unionFind('User', 'LINK', {
  graph: 'wcc-graph',
  write: true,
  writeProperty: "componentId",
  weightProperty: 'weight',
  threshold: 1.0
})
YIELD nodes AS Nodes, setCount AS NbrOfComponents, writeProperty AS PropertyName;
----

.Results
[opts="header",cols="1m,1m,1m"]
|===
| Nodes | NbrOfComponents | PropertyName
| 6     | 3               | "componentId"
|===

As we can see from the results, Bridget is now in their own component, due to the relationship weight not being above the configured threshold.


[[algorithms-wcc-examples-seeding]]
=== Seeded components

It is possible to define preliminary component IDs for nodes using the `seedProperty` configuration parameter.
This is helpful if we want to retain components from a previous run.
The property value needs to be a number.

The algorithm first checks if there is a seeded component ID assigned to the node.
If there is one, that component ID is used.
Otherwise, a new unique component ID is assigned to the node.

Once every node belongs to a component, the algorithm merges components of connected nodes.
When components are merged, the resulting component is always the one with the lower component ID.

[NOTE]
If the `seedProperty` configuration parameter has the same value as `writeProperty`, the algorithm only writes properties for nodes where the component ID has changed.
If they differ, the algorithm writes properties for all nodes.

//TODO: add some Cypher and show an actual example


[[algorithms-wcc-imp]]
== Implementation

`algo.unionFind`

* If a threshold configuration parameter is supplied, only relationships with a property value higher than the threshold are merged.
* Parallel `Union Find`, using `ExecutorService` only.
* Algorithm based on the idea that `DisjointSetStruct` can be built using just a partition of the nodes, which are then merged pairwise.
* The implementation is based on a queue which acts as a buffer for each computed `DisjointSetStruct`.
  As long as there are more elements on the queue, the algorithm takes two, merges them, and adds its result to the queue until only 1 element remains.


ifndef::env-docs[]
== References

// tag::references[]

* http://math.hws.edu/eck/cs327_s04/chapter9.pdf
* https://en.wikipedia.org/wiki/Connected_component_(graph_theory)

// end::references[]
endif::env-docs[]

ifdef::implementation[]
// tag::implementation[]


== Implementation details

:leveloffset: +1
// copied from: https://github.com/neo4j-contrib/neo4j-graph-algorithms/issues/79

_Connected Components_ or _Union Find_ basically finds sets of connected nodes where each node is reachable from any other node in the same set. One implementation also evaluates a Predicate on each relation which allows partitioning of the graph based on Relationships and Properties.

## Progress

- [x] single threaded implementation
- [x] tests
- [x] simple benchmark
- [x] implement procedure
- [x] benchmark on bigger graphs
- [x] parallelization
- [x] evaluation

## Requirements

`AllRelationshipIterator` & `Weights`

## Data structured involved

We use a disjoint-set-structure which is based on a parent-array-tree. The DSS can be used to efficiently ask if two nodes are reachable by each other. [More](https://en.wikipedia.org/wiki/Disjoint-set_data_structure)

## ToDo

### benchmark

Implement benchmark on big graph &

- stream nodeId-setId pairs
- calculate setSize-setCount


### parallelization

One approach to parallelize _Union Find_ might be _relationship partitioning_ where each thread performs the execution into it's own DSS instance on a subset of relationships. So each thread calculates a distinct set of unions. Later we can merge each DSS pairwise which can also be perfomed in parallel. Nonetheless the memory consumption might be high due to the preallocated array in DSS. We could also switch to a growing container if this is a problem.

### evaluation

- Performance tests on different dataset sizes / level of concurrency


== Details

- writes a cluster-id to each node representing the a connected component where each node
is reachable from any other node


=== algo.unionFind

- if a threshold configuration parameter is supplied only relationships with a property value higher then the threshold
are merged


=== algo.unionFind.queue

- parallel Union Find using ExecutorService only.
- Algorithm based on the idea that DisjointSetStruct can be built using just a partition of the nodes
which are then merged pairwise.
- The implementation is based on a queue which acts as a buffer for each computed DSS. As long as there are
more elements on the queue the algorithm takes two, merges them and adds its result to the queue until only
1 element remains.


=== algo.unionFind.forkJoinMerge

-  Like in *exp1* the resulting DSS of each node-partition is merged by the ForkJoin pool while
the calculation of the DSS is done by the ExecutorService.


=== algo.unionFind.forkJoin

- calculation and merge using forkJoinPool

// end::implementation[]
endif::implementation[]
