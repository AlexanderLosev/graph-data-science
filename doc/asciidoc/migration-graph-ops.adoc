[[graph-ops]]
= Graph operations

. <<migration-estimate, Memory estimation>>
. <<migration-named-graph, Graph creation - Named Graph>>
. <<migration-cypher-queries, Graph creation - Cypher Queries>>
. <<migration-graph-list, Graph listing>>
. <<migration-graph-info, Graph info>>
. <<migration-graph-remove, Graph removal>>


[[migration-estimate]]
== Memory estimation

.Changes in the YIELD fields
[opts=header,cols="1,1"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `requiredMemory`    | `requiredMemory`
| `bytesMin`          | `bytesMin`
| `bytesMax`          | `bytesMax`
| `mapView`           | `mapView`
| -                 | `treeView`
| -                 | `nodeCount`
| -                 | `relationshipCount`
|===

The most significant change in memory estimation is that in GDS to estimate an operation you suffix it with `.estimate` while in GA the operation had to be passed as parameter to `algo.memrec`.

.Estimating the memory requirements of loading a named graph:
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Native Projections
|
[source, cypher]
----
CALL algo.memrec(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  'graph.load'
)
----
|
[source, cypher]
----
CALL gds.graph.create.estimate(
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
2+| Cypher Projections
|
[source, cypher]
----
CALL algo.memrec(
  'MATCH (n:MyLabel) RETURN id(n) AS id',
  'MATCH (s)-[r:MY_RELATIONSHIP_TYPE]->(t)
   RETURN id(s) AS source, id(t) AS target',
  'graph.load',
  {
    graph: 'cypher'
  }
)
----
|
[source, cypher]
----
CALL gds.graph.create.cypher.estimate(
  'MATCH (n:MyLabel) RETURN id(n) AS id',
  'MATCH (s)-[r:MY_RELATIONSHIP_TYPE]->(t)
   RETURN id(s) AS source, id(t) AS target'
)
----
|===

[[migration-named-graph]]
== Graph creation - Named Graph

.Changes in the YIELD fields
[opts=header, cols="1,1"]
|====
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `name`                      | `graphName`
| `graph`                     | -
| `direction`                 | -
| `undirected`                | -
| `sorted`                    | -
| `nodes`                     | `nodesCount`
| `loadMillis`                | `createMillis`
| `alreadyLoaded`             | -
| `nodeProperties`            | -
| `relationshipProperties`    | `relationshipCount`
| `relationshipWeight`        | -
| `loadNodes`                 | -
| `loadRelationships`         | -
| -                         | `nodeProjection`
| -                         | `relationshipProjection`
|====
.Loading a named graph in the default way:
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Minimal Native Projection
|
[source, cypher]
----
CALL algo.graph.load(
  'myGraph',
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
|
[source, cypher]
----
CALL gds.graph.create(
  'myGraph',
  'MyLabel',
  'MY_RELATIONSHIP_TYPE'
)
----
2+| Native Projection with additional properties
|
[source, cypher]
----
CALL algo.graph.load(
  'myGraph',
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  {
    concurrency: 4,
    graph: 'huge',
    direction: 'INCOMING'
  }
)
----
|
[source, cypher]
----
CALL gds.graph.create(
  'myGraph',
  'MyLabel',
  {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'REVERSE'
    }
  },
  {
    readConcurrency: 4
  }
)
----
2+|[[direction-both-example]]Native Projection with `direction: 'BOTH'`
|
[source, cypher]
----
CALL algo.graph.load(
  'myGraph',
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  {
    graph: 'huge',
    direction: 'BOTH'
  }
)
----
|
[source, cypher]
----
CALL gds.graph.create(
  'myGraph',
  'MyLabel',
  {
    MY_RELATIONSHIP_TYPE_NATURAL: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'NATURAL'
    },
    MY_RELATIONSHIP_TYPE_REVERSE: {
      type: 'MY_RELATIONSHIP_TYPE',
      orientation: 'REVERSE'
    }
  }
)
----
2+| Undirected Native Projection
|
[source, cypher]
----
CALL algo.graph.load(
  'myGraph',
  'MyLabel',
  'MY_RELATIONSHIP_TYPE',
  {
    graph: 'huge',
    undirected: true
  }
)
----
|
[source, cypher]
----
CALL gds.graph.create(
  'myGraph',
  'MyLabel',
  {
    MY_RELATIONSHIP_TYPE: {
      orientation: 'UNDIRECTED'
    }
  }
)
----
|===

[[migration-cypher-queries]]
== Graph creation - Cypher Queries

.Loading a named graph using Cypher queries:
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| Basic Cypher queries, defining source and target
|
[source, cypher]
----
CALL algo.graph.load(
  'myGraph',
  'MATCH (n:MyLabel)
   RETURN id(n) AS id',
  'MATCH (s)-[r:MY_RELATIONSHIP_TYPE]->(t)
   RETURN id(s) AS source, id(t) AS target',
  {
    graph: 'cypher'
  }
)
----
|
[source, cypher]
----
CALL gds.graph.create.cypher(
  'myGraph',
  'MATCH (n:MyLabel)
   RETURN id(n) AS id',
  'MATCH (s)-[r:MY_RELATIONSHIP_TYPE]->(t)
   RETURN id(s) AS source, id(t) AS target'
)
----
2+| With concurrency property and Cypher query with relationship property
|
[source, cypher]
----
CALL algo.graph.load(
  'myGraph',
  'MATCH (n:MyLabel)
   RETURN id(n) AS id',
  'MATCH (s)-[r:MY_RELATIONSHIP_TYPE]->(t)
   RETURN
     id(s) AS source,
     id(t) AS target,
     r.myProperty AS weight',
  {
    concurrency: 4,
    graph: 'cypher'
  }
)
----
|
[source, cypher]
----
CALL gds.graph.create.cypher(
  'myGraph',
  'MATCH (n:MyLabel)
   RETURN id(n) AS id',
  'MATCH (s)-[r:MY_RELATIONSHIP_TYPE]->(t)
   RETURN
     id(s) AS source,
     id(t) AS target,
     r.myProperty AS weight',
  {
    readConcurrency: 4
  }
)
----
|===

[[migration-graph-list]]
== Graph listing

.Changes in the YIELD fields
[opts=header,cols="1,1"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `name`              | `graphName`
| `nodes`             | `nodeCount`
| `relationships`     | `relationshipCount`
| `type`              | -
| `direction`         | -
| -                 | `nodeProjection`
| -                 | `relationshipProjection`
| -                 | `nodeQuery` footnote:list-cypher[Field will be `null` unless a Cypher projection was used]
| -                 | `relationshipQuery` footnote:list-cypher[]
| -                 | `degreeDistribution` footnote:[Graph statistics map, i.e. min, max, percentiles, etc.]
|===

.Listing named graphs:
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
|
[source, cypher]
----
CALL algo.graph.list()
----
|
[source, cypher]
----
CALL gds.graph.list()
----
|===

[[migration-graph-info]]
== Graph info

.Changes in the YIELD fields
[opts=header,cols="1,1"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `name`              | `graphName`
| `nodes`             | `nodeCount`
| `relationships`     | `relationshipCount`
| `exists`            | -
| `removed`           | -
| `type`              | -
| `direction`         | -
| -                 | `nodeProjection`
| -                 | `relationshipProjection`
| -                 | `nodeQuery` footnote:info-cypher[Field will be `null` unless a Cypher projection was used]
| -                 | `relationshipQuery` footnote:info-cypher[]
| -                 | `degreeDistribution` footnote:[Graph statistics map, i.e. min, max, percentiles, etc.]
| `min, max, mean, p50, p75, p90, p95, p99, p999` footnote:info-degreeDistribution[Inlined into `degreeDistribution`]              | -
|===

.Viewing information about a specific named graph:
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
2+| View information for a Named graph
|
[source, cypher]
----
CALL algo.graph.info('myGraph')
----
|
[source, cypher]
----
CALL gds.graph.list('myGraph')
----
2+| Check graph existence
|
[source, cypher]
----
CALL algo.graph.info('myGraph') YIELD exists
----
|
[source, cypher]
----
CALL gds.graph.exists('myGraph') YIELD exists
----
2+| View graph statistics
|
[source, cypher]
----
CALL algo.graph.info('myGraph', true)
YIELD min, max, mean, p50
----
|
[source, cypher]
----
CALL gds.graph.list('myGraph')
YIELD degreeDistribution as dd
RETURN dd.min, dd.max, dd.mean, dd.p50
----
|===

[[migration-graph-remove]]
== Graph removal

.Changes in the YIELD fields
[opts=header,cols="1,1"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
| `name`              | `graphName`
| `nodes`             | `nodeCount`
| `relationships`     | `relationshipCount`
| `exists`            | -
| `removed`           | -
| `type`              | -
| `direction`         | -
| -                 | `nodeProjection`
| -                 | `relationshipProjection`
| -                 | `nodeQuery` footnote:remove-cypher[Field will be `null` unless a Cypher projection was used]
| -                 | `relationshipQuery` footnote:remove-cypher[]
| -                 | `degreeDistribution`
|===

.Removing a named graph:
[opts=header,cols="1a,1a"]
|===
|Graph Algorithms v3.5 |Graph Data Science v1.0
|
[source, cypher]
----
CALL algo.graph.remove('myGraph')
----
|
[source, cypher]
----
CALL gds.graph.drop('myGraph')
----
|===