[[algorithms-label-propagation]]
= Label Propagation
:entity: node
:result: community ID

[abstract]
--
This section describes the Label Propagation algorithm in the Neo4j Graph Data Science library.
--

The Label Propagation algorithm (LPA) is a fast algorithm for finding communities in a graph.
It detects these communities using network structure alone as its guide, and doesn't require a pre-defined objective function or prior information about the communities.

One interesting feature of LPA is that nodes can be assigned preliminary labels to narrow down the range of solutions generated.
This means that it can be used as semi-supervised way of finding communities where we hand-pick some initial communities.

This section includes:

* <<algorithms-label-propagation-intro, Introduction>>
* <<algorithms-label-propagation-syntax, Syntax>>
* <<algorithms-label-propagation-examples, Examples>>
** <<algorithms-label-propagation-examples-unweighted, Unweighted>>
** <<algorithms-label-propagation-examples-weighted, Weighted>>
** <<algorithms-label-propagation-examples-seeded, Seeded>>

[[algorithms-label-propagation-intro]]
== Introduction

LPA works by propagating labels throughout the network and forming communities based on this process of label propagation.

The intuition behind the algorithm is that a single label can quickly become dominant in a densely connected group of nodes, but will have trouble crossing a sparsely connected region.
Labels will get trapped inside a densely connected group of nodes, and those nodes that end up with the same label when the algorithms finish can be considered part of the same community.

The algorithm works as follows:

* Every node is initialized with a unique community label (an identifier).
* These labels propagate through the network.
* At every iteration of propagation, each node updates its label to the one that the maximum numbers of its neighbours belongs to.
  Ties are broken arbitrarily but deterministically.
* LPA reaches convergence when each node has the majority label of its neighbours.
* LPA stops if either convergence or the user-defined maximum number of iterations is achieved.

As labels propagate, densely connected groups of nodes quickly reach a consensus on a unique label.
At the end of the propagation only a few labels will remain - most will have disappeared.
Nodes that have the same community label at convergence are said to belong to the same community.

For more information on this algorithm, see:

* https://arxiv.org/pdf/0709.2938.pdf["Near linear time algorithm to detect community structures in large-scale networks"^]
* Use cases:
** https://dl.acm.org/citation.cfm?id=2140465[Twitter polarity classification with label propagation over lexical links and the follower graph]
** https://www.nature.com/articles/srep12339[Label Propagation Prediction of Drug-Drug Interactions Based on Clinical Side Effects]
** https://www.uni-ulm.de/fileadmin/website_uni_ulm/iui.iwsds2017/papers/IWSDS2017_paper_12.pdf["Feature Inference Based on Label Propagation on Wikidata Graph for DST"]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====

[[algorithms-label-propagation-syntax]]
== Syntax

=== Write mode

.Run Label Propagation in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.labelPropagation.write(
  graphName: String,
  configuration: Map
)
YIELD
  // general write return columns
  ranIterations: Integer,
  didConverge: Boolean
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run Label Propagation in write mode on an anonymous graph.
[source, cypher]
----
CALL gds.labelPropagation.write(configuration: Map)
YIELD
  // general write return columns
  ranIterations: Integer,
  didConverge: Boolean
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                       | Type    | Default | Optional | Description
| maxIterations              | Integer | 10      | yes      | The maximum number of iterations to run.
| nodeWeightProperty         | String  | null    | yes      | The name of the node property that represents weight.
| relationshipWeightProperty | String  | null    | yes      | The name of the relationship property that represents weight.
| seedProperty               | String  | n/a     | yes      | Used to define initial set of labels (must be a number).
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type    | Description
| createMillis          | Integer | Milliseconds for loading data.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| writeMillis           | Integer | Milliseconds for writing result data back.
| postProcessingMillis  | Integer | Milliseconds for computing percentiles and community count.

| nodePropertiesWritten | Integer | The number of node properties written.
| communityCount        | Integer | The number of communities found.
| ranIterations         | Integer | The number of iterations that were executed.
| didConverge           | Boolean | True if the algorithm did converge to a stable labelling within the provided number of maximum iterations.

| communityDistribution | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size.
| configuration         | Map     | The configuration used for running the algorithm.

|===


=== Mutate mode

.Run Label Propagation in mutate mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.labelPropagation.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  // general mutate return columns
  ranIterations: Integer,
  didConverge: Boolean
----

The configuration for the `mutate` mode is equivalent to the `write` mode.
Instead of specifying a `writeProperty`, we need to specify a `mutateProperty`.
Also, specifying `writeConcurrency` is not possible in `mutate` mode.

.The following will run the algorithm and store the results in `myGraph`:
[source, cypher]
----
CALL gds.labelPropagation.mutate('myGraph', { mutateProperty: 'communityId' })
----


=== Stream mode

The following will run the algorithm and stream back results:

.Run Label Propagation in stream mode on a graph stored in the catalog.
[source,cypher]
----
CALL gds.labelPropagation.stream(
  graphName: String,
  configuration: Map
)
YIELD
    nodeId: Integer,
    communityId: Integer
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run Label Propagation in stream mode on an anonymous graph.
[source,cypher]
----
CALL gds.labelPropagation.stream(configuration: Map)
YIELD
    nodeId: Integer,
    communityId: Integer
----

include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                       | Type    | Default                | Optional | Description
| maxIterations              | Integer | 10                     | yes      | The maximum number of iterations to run.
| nodeWeightProperty         | String  | null                   | yes      | The property name of node that contain weight. Must be numeric.
| relationshipWeightProperty | String  | null                   | yes      | The property name of relationship that contain weight. Must be numeric.
| seedProperty               | String  | n/a                    | yes      | Used to define initial set of labels (must be a number).
|===

.Results
[opts="header"]
|===
| Name        | Type    | Description
| nodeId      | Integer | Node ID
| communityId | Integer | Community ID
|===


=== Stats mode

.Run Label Propagation in stats mode on an named graph.
[source, cypher]
----
CALL gds.labelPropagation.stats(
  graphName: String,
  configuration: Map
)
YIELD
  ranIterations: Integer,
  didConverge: Boolean,
  createMillis: Integer,
  computeMillis: Integer
----

include::common-parameters-named-graph.adoc[]

.Run Label Propagation in stats mode on an anonymous graph.
[source, cypher]
----
CALL gds.labelPropagation.stats(configuration: Map)
YIELD
  ranIterations: Integer,
  didConverge: Boolean,
  createMillis: Integer,
  computeMillis: Integer
----

include::common-parameters-anonymous-graph.adoc[]

The configuration is the same as for the `write` mode.

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type    | Description
| createMillis          | Integer | Milliseconds for loading data.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| postProcessingMillis  | Integer | Milliseconds for computing percentiles and community count.
| communityCount        | Integer | The number of communities found.
| ranIterations         | Integer | The number of iterations that were executed.
| didConverge           | Boolean | True if the algorithm did converge to a stable labelling within the provided number of maximum iterations.
| communityDistribution | Map     | Map containing min, max, mean as well as p50, p75, p90, p95, p99 and p999 percentile values of community size.
| configuration         | Map     | The configuration used for running the algorithm.
|===


=== Estimate mode

The following will estimate the memory requirements for running the algorithm.
The `mode` can be substituted with the available modes (`stream`, `write` and `stats`).

.Run Label Propagation in estimate mode on an named graph.
[source,cypher]
----
CALL gds.labelPropagation.<mode>.estimate(
  graphName: String,
  configuration: Map
)
----

include::common-parameters-named-graph.adoc[]

.Run Label Propagation in estimate mode on an named graph.
[source,cypher]
----
CALL gds.labelPropagation.<mode>.estimate(configuration: Map)
----

include::common-parameters-anonymous-graph.adoc[]

include::common-estimate-mode.adoc[]

[[algorithms-label-propagation-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE (alice:User {name: 'Alice', seed_label: 52})
CREATE (bridget:User {name: 'Bridget', seed_label: 21})
CREATE (charles:User {name: 'Charles', seed_label: 43})
CREATE (doug:User {name: 'Doug', seed_label: 21})
CREATE (mark:User {name: 'Mark', seed_label: 19})
CREATE (michael:User {name: 'Michael', seed_label: 52})

CREATE (alice)-[:FOLLOW {weight: 1}]->(bridget)
CREATE (alice)-[:FOLLOW {weight: 10}]->(charles)
CREATE (mark)-[:FOLLOW {weight: 1}]->(doug)
CREATE (bridget)-[:FOLLOW {weight: 1}]->(michael)
CREATE (doug)-[:FOLLOW {weight: 1}]->(mark)
CREATE (michael)-[:FOLLOW {weight: 1}]->(alice)
CREATE (alice)-[:FOLLOW {weight: 1}]->(michael)
CREATE (bridget)-[:FOLLOW {weight: 1}]->(alice)
CREATE (michael)-[:FOLLOW {weight: 1}]->(bridget)
CREATE (charles)-[:FOLLOW {weight: 1}]->(doug)
----

This graph represents six users, some of whom follow each other.
Besides a `name` property, each user also has a `seed_label`  property.
The `seed_label` property represents a value in the graph used to seed the node with a label.
For example, this can be a result from a previous run of the Label Propagation algorithm.
In addition, each relationship has a weight property.

[NOTE]
====
In the examples below we will use named graphs and standard projections as the norm.
However, <<cypher-projection, Cypher projection>> and anonymous graphs could also be used.
====

.The following statement will create the graph and store it in the graph catalog.
[source, cypher, role=setup-query]
----
CALL gds.graph.create(
    'myGraph',
    'User',
    'FOLLOW',
    {
        nodeProperties: 'seed_label',
        relationshipProperties: 'weight'
    }
)
----

In the following examples we will demonstrate using the Label Propagation algorithm on this graph.

[[algorithms-label-propagation-examples-unweighted]]
=== Unweighted

[role=query-example]
--
.The following will run the algorithm and stream results:
[source, cypher]
----
CALL gds.labelPropagation.stream('myGraph')
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name    | Community
| "Alice"   | 1
| "Bridget" | 1
| "Michael" | 1
| "Charles" | 4
| "Doug"    | 4
| "Mark"    | 4
|===
--


[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write('myGraph', { writeProperty: 'community' })
YIELD ranIterations, communityCount
----

.Results
[opts="header",cols="1m,1m"]
|===
| ranIterations | communityCount
| 3             | 2
|===
--

Our algorithm found two communities, with 3 members each.

It appears that Michael, Bridget, and Alice belong together, as do Doug and Mark.
Only Charles doesn't strongly fit into either side, but ends up with Doug and Mark.

[[algorithms-label-propagation-examples-weighted]]
=== Weighted

The Label-Propagation algorithm can also take node and relationship weights into account.
When we created `myGraph`, we projected the relationship property `weight`.
In order to tell the algorithm to consider this property as a relationship weight, we have to set the `relationshipWeightProperty` configuration parameter to `weight`.

[role=query-example]
--
.The following will run the algorithm on a graph with weighted relationships and stream results:
[source, cypher]
----
CALL gds.labelPropagation.stream('myGraph', { relationshipWeightProperty: 'weight' })
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name      | Community
| "Bridget" | 2
| "Michael" | 2
| "Alice"   | 4
| "Charles" | 4
| "Doug"    | 4
| "Mark"    | 4
|===
--

Using the weighted relationships, `Alice` and `Charles` are now in the same community as there is a strong link between them.

[role=query-example]
--
.The following will run the algorithm on a weighted graph and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write('myGraph', {
  writeProperty: 'community',
  relationshipWeightProperty: 'weight'
})
YIELD ranIterations, communityCount
----

.Results
[opts="header",cols="1m,1m"]
|===
| ranIterations | communityCount
| 4             | 2
|===
--

As we can see, the weighted example takes 4 iterations to converge, instead of 3 for the unweighted case.

Additionally by specifying a node weight via the `nodeWeightProperty` key, we can control the influence of a nodes community onto its neighbors.
During the computation of the weight of a specific community, the node property will be multiplied by the weight of that nodes relationships.

[[algorithms-label-propagation-examples-seeded]]
=== Seeded

At the beginning of the algorithm, every node is initialized with a unique label and the labels propagate through the network.

An initial set of labels can be provided by setting the `seedProperty` configuration parameter.
When we created `myGraph`, we projected the node property `seed_label`.
We can use this node property as `seedProperty`.

The algorithm first checks if there is a seed label assigned to the node.
If no seed label is present, a new unique label is assigned to the node.
Using this preliminary set of labels, it then sequentially updates each node's label to a new one, which is the most frequent label among its neighbors at every iteration of label propagation.

[role=query-example]
--
.The following will run the algorithm with pre-defined labels:
[source, cypher]
----
CALL gds.labelPropagation.stream('myGraph', { seedProperty: 'seed_label' })
YIELD nodeId, communityId AS Community
RETURN gds.util.asNode(nodeId).name AS Name, Community
ORDER BY Community, Name
----

.Results
[opts="header",cols="1,1"]
|===
| Name      | Community
| "Charles" | 19
| "Doug"    | 19
| "Mark"    | 19
| "Alice"   | 21
| "Bridget" | 21
| "Michael" | 21
|===
--

As we can see, the communities are based on the `seed_label` property, concretely `19` is from the user `Mark` and `21` from `Doug`.

[role=query-example]
--
.The following will run the algorithm and write back results:
[source, cypher]
----
CALL gds.labelPropagation.write('myGraph', {
  writeProperty: 'community',
  seedProperty: 'seed_label'
})
YIELD ranIterations, communityCount
----

.Results
[opts="header",cols="1m,1m"]
|===
| ranIterations | communityCount
| 3             | 2
|===
--
