[[algorithms-embeddings-graph-sage]]
[.beta]
= GraphSAGE

[abstract]
--
This section describes the GraphSAGE node embedding algorithm in the Neo4j Graph Data Science library.
--

GraphSAGE is an _inductive_ algorithm for computing node embeddings.
GraphSAGE is using node feature information to generate node embeddings on unseen nodes or graphs.
Instead of training individual embeddings for each node, the algorithm learns a function that generates embeddings by sampling and aggregating features from a node's local neighborhood.

NOTE: The algorithm is defined for UNDIRECTED graphs.

For more information on this algorithm see:

* https://arxiv.org/pdf/1706.02216.pdf[William L. Hamilton, Rex Ying, and Jure Leskovec. "Inductive Representation Learning on Large Graphs." 2018.^]
* https://arxiv.org/pdf/1911.10232.pdf[Amit Pande, Kai Ni and Venkataramani Kini. "SWAG: Item Recommendations using Convolutions on Weighted Graphs." 2019.^]

[[algorithms-embeddings-graph-sage-syntax]]
== Syntax

.GraphSAGE syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======
.Run GraphSAGE in stream mode on a named graph.
[source, cypher]
----
CALL gds.beta.graphSage.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  embedding: List<Float>
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name      | Type         | Description
| nodeId    | Integer      | The Neo4j node ID.
| embedding | List<Float>  | The computed node embedding.
|===
======

[.include-with-mutate]
======
.Run GraphSAGE in mutate mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.beta.graphSage.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  configuration: Map
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type                 | Description
| nodesCount            | Integer              | The number of nodes processed.
| nodePropertiesWritten | Integer              | The number of node properties written.
| createMillis          | Integer              | Milliseconds for loading data.
| computeMillis         | Integer              | Milliseconds for running the algorithm.
| mutateMillis          | Integer              | Milliseconds for writing result data back to the in-memory graph.
| configuration         | Map                  | The configuration used for running the algorithm.
|===
======

[.include-with-write]
======
.Run GraphSAGE in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.beta.graphSage.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type                 | Description
| nodesCount            | Integer              | The number of nodes processed.
| nodePropertiesWritten | Integer              | The number of node properties written.
| createMillis          | Integer              | Milliseconds for loading data.
| computeMillis         | Integer              | Milliseconds for running the algorithm.
| writeMillis           | Integer              | Milliseconds for writing result data back to Neo4j.
| configuration         | Map                  | The configuration used for running the algorithm.
|===
======

[.include-with-train]
======
.Run GraphSAGE in train mode on a named graph.
[source, cypher]
----
CALL gds.beta.graphSage.train(
  graphName: String,
  configuration: Map
) YIELD
  graphName: String,
  modelInfo: Map,
  configuration: Map,
  trainMillis: Integer
----

include::../../common-configuration/common-parameters-named-graph.adoc[]

include::../../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-train-configuration.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name          | Type    | Description
| graphName     | String  | The name of the in-memory graph used during training
| modelInfo     | Map     | Details of the trained model
| configuration | Map     | The configuration used to run the procedure
| trainMillis   | Integer | Milliseconds to train the model
|===
======
====


[[algorithms-embeddings-graph-sage-syntax-anonymous]]
=== Anonymous graphs

include::../../shared/syntax-anonymous-graphs.adoc[]

.Run GraphSAGE in write mode on an anonymous graph.
[source, cypher]
----
CALL gds.beta.graphSage.write(
  configuration: Map
)
YIELD
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  nodeCount: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../../common-configuration/common-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-embeddings-graph-sage-syntax, write mode syntax above>>.

[[algorithms-embeddings-graph-sage-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE
  (Dan:Person{ name: 'Dan', age: 20, height: 185, weight: 75 }),
  (Annie:Person{ name: 'Annie', age: 12, height: 124, weight: 42 }),
  (Matt:Person{ name: 'Matt', age: 67, height: 170, weight: 80 }),
  (Jeff:Person{ name: 'Jeff', age: 45, height: 192, weight: 85 }),
  (Brie:Person{ name: 'Brie', age: 27, height: 176, weight: 57 }),
  (Elsa:Person{ name: 'Elsa', age: 32, height: 158, weight: 55 }),
  (John:Person{ name: 'John', age: 35, height: 172, weight: 76 }),

  (Dan)-[:REL { relWeight: 1.0 }]->(Annie),
  (Dan)-[:REL { relWeight: 1.6 }]->(Matt),
  (Annie)-[:REL { relWeight: 0.1 }]->(Matt),
  (Annie)-[:REL { relWeight: 3.0 }]->(Jeff),
  (Annie)-[:REL { relWeight: 1.2 }]->(Brie),
  (Matt)-[:REL { relWeight: 10.0 }]->(Brie),
  (Brie)-[:REL { relWeight: 1.0 }]->(Elsa),
  (Brie)-[:REL { relWeight: 2.2 }]->(Jeff),
  (John)-[:REL { relWeight: 5.0 }]->(Jeff);
----

[source, cypher, role=graph-create-query]
----
CALL gds.graph.create(
  'persons',
  {
    Person: {
      label: 'Person',
      properties: ['age', 'height', 'weight']
    }
  }, {
    R: {
      type: 'REL',
      orientation: 'UNDIRECTED',
      properties: ['relWeight']
    }
})
----

NOTE: The algorithm is defined for UNDIRECTED graphs.


=== Train

Before we are able to generate node embeddings we need to train a model.
Below is an example of how to do that.

NOTE: The names specified in the `featureProperties` configuration parameter must exist in the in-memory graph.

[source, cypher, role=query-example, no-result=true]
----
CALL gds.beta.graphSage.train(
  'persons',
  {
    modelName: 'exampleTrainModel',
    featureProperties: ['age', 'height', 'weight'],
    aggregator: 'mean',
    activationFunction: 'sigmoid',
    sampleSizes: [25, 10],
    degreeAsProperty: true
  }
)
----

WARNING: The `train` procedure will raise an error if there are no relationships in the graph.

=== Train with relationship weights

The GraphSage implementation supports training using relationship weights.
Greater relationship weight between nodes signifies that the nodes should have more similar embedding values.

[source, cypher, role=query-example, no-result=true]
----
CALL gds.beta.graphSage.train(
  'persons',
  {
    modelName: 'weightedTrainedModel',
    featureProperties: ['age', 'height', 'weight'],
    aggregator: 'mean',
    activationFunction: 'sigmoid',
    sampleSizes: [25, 10],
    degreeAsProperty: true,
    relationshipWeightProperty: 'relWeight'
  }
)
----

Relationship weights are only used during GraphSage training.
The trained models can then be used to stream or write back the generated node embeddings.


=== Stream

To generate embeddings and stream them back to the client we can use the stream mode.
We must first train a model, which we do using the `gds.beta.graphSage.train` procedure.

[[graph-sage-trained-model-example]]
[source, cypher, role=query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.train(
  'persons',
  {
    modelName: 'graphSage',
    featureProperties: ['age', 'height', 'weight'],
    embeddingDimension: 3,
    degreeAsProperty: true
  }
)
----

Once we have trained a model (named `'graphSage'`) we can use it to generate and stream the embeddings.

[source, cypher, role=query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.stream(
  'persons',
  {
    modelName: 'graphSage'
  }
)
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [0.5773502692664537,0.5773502691669364,0.5773502691354871]
| 1      | [0.5773502692979483,0.5773502691576353,0.5773502691132936]
| 2      | [0.5773502692500592,0.5773502691717781,0.5773502691470399]
| 3      | [0.5773502692751916,0.577350269164356,0.5773502691293296]
| 4      | [0.5773502693595794,0.577350269139434,0.5773502690698639]
| 5      | [0.5773502693858079,0.5773502691316881,0.5773502690513813]
| 6      | [0.577350269340378,0.5773502691451048,0.5773502690833946]
|===

NOTE: Due to the random initialisation of the weight variables the results may vary slightly between the runs.

=== Mutate

The <<graph-sage-trained-model-example, model trained as part of the stream example>> can be reused to write the results to the in-memory graph using the `mutate` mode of the procedure.
Below is an example of how to achieve this.

--
[source, cypher, role=query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.mutate(
  'persons',
  {
    mutateProperty: 'inMemoryEmbedding',
    modelName: 'graphSage'
  }
) YIELD
  nodeCount,
  nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodeCount | nodePropertiesWritten
| 7         | 7
|===
--

=== Write

The <<graph-sage-trained-model-example, model trained as part of the stream example>> can be reused to write the results to Neo4j.
Below is an example of how to achieve this.

--
[source, cypher, role=query-example, no-result=true, group=graphSage]
----
CALL gds.beta.graphSage.write(
  'persons',
  {
    writeProperty: 'embedding',
    modelName: 'graphSage'
  }
) YIELD
  nodeCount,
  nodePropertiesWritten
----

[opts=header]
.Results
|===
| nodeCount | nodePropertiesWritten
| 7         | 7
|===
--
