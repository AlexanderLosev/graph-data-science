[[algorithms-node-similarity]]
= Node Similarity
:entity: relationship
:result: similarity score

[abstract]
--
This section describes the Node Similarity algorithm in the Neo4j Graph Data Science library.
The algorithm is based on the Jaccard Similarity score.
--

This topic includes:

* <<algorithms-node-similarity-intro, Introduction>>
* <<algorithms-node-similarity-syntax, Syntax>>
* <<algorithms-node-similarity-examples, Examples>>
** <<algorithms-node-similarity-examples-stream, Streaming results>>
** <<algorithms-node-similarity-examples-write, Writing results>>
** <<algorithms-node-similarity-examples-limits, Limiting results>>
*** <<algorithms-node-similarity-examples-limits-topk-bottomk, topK and bottomK>>
*** <<algorithms-node-similarity-examples-limits-topn-bottomn, topN and bottomN>>
** <<algorithms-node-similarity-examples-degree-similarity-cutoff, Degree cutoff and similarity cutoff>>
** <<algorithms-node-similarity-examples-memory-estimation, Memory Estimation>>
** <<algorithms-node-similarity-examples-stats, Stats>>

[[algorithms-node-similarity-intro]]
== Introduction

The Node Similarity algorithm compares a set of nodes based on the nodes they are connected to.
Two nodes are considered similar if they share many of the same neighbors.
Node Similarity computes pair-wise similarities based on the Jaccard metric, also known as the Jaccard Similarity Score.

Jaccard Similarity is computed using the following formula:

image::jaccard.png[role="middle"]
// This is the raw information for this image:
// // J(A,B) = ∣A ∩ B∣ / ∣(A ∪ B)∣ // = ∣A ∩ B∣ / ∣A∣ + ∣B∣ - ∣A ∩ B| //

The input of this algorithm is a bipartite, connected graph containing two disjoint node sets.
Each relationship starts from a node in the first node set and ends at a node in the second node set.
The Node Similarity algorithm compares all nodes from the first node set with each other based on their relationships to nodes in the second set.
The complexity of this comparison grows quadratically with the number of nodes to compare.
The algorithm reduces the complexity by ignoring disconnected nodes.

In addition to computational complexity, the memory requirement for producing results also scales roughly quadratically.
In order to bound memory usage, the algorithm requires an explicit limit on the number of results to compute per node.
This is the 'topK' parameter.
It can be set to any value, except 0.

The output of the algorithm are new relationships between pairs of the first node set.
Similarity scores are expressed via relationship properties.

A related function for computing Jaccard similarity is described in <<alpha-algorithms-similarity-jaccard>>.

For more information on this algorithm, see:

* https://en.wikipedia.org/wiki/Similarity_(network_science)#Structural_equivalence[Structural equivalence (Wikipedia)]
* https://en.wikipedia.org/wiki/Jaccard_index[The Jaccard index (Wikipedia)].
* https://en.wikipedia.org/wiki/Bipartite_graph[Bipartite graphs (Wikipedia)]

[NOTE]
====
Running this algorithm requires sufficient available memory.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====


[[algorithms-node-similarity-syntax]]
== Syntax


=== Write mode

.Run Node Similarity in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.nodeSimilarity.write(
  graphName: String,
  configuration: Map
)
YIELD
  // general write return columns
  nodesCompared: Integer,
  relationshipsWritten: Integer,
  writeRelationshipType: String,
  writeProperty: String
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run Node Similarity in write mode on an anonymous graph.
[source, cypher]
----
CALL gds.nodeSimilarity.write(configuration: Map)
YIELD
  // general write return columns
  nodesCompared: Integer,
  relationshipsWritten: Integer
----

include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name                   | Type    | Default                | Optional | Description
| similarityCutoff       | Float   | 1E-42                  | yes      | Lower limit for the similarity score to be present in the result. Values must be between 0 and 1.
| degreeCutoff           | Integer | 1                      | yes      | Lower limit on the node degree for a node to be considered in the comparisons. This value cannot be lower than 1.
| topK                   | Integer | 10                     | yes      | Limit on the number of scores per node. The K largest results are returned. This value cannot be lower than 1.
| bottomK                | Integer | 10                     | yes      | Limit on the number of scores per node. The K smallest results are returned. This value cannot be lower than 1.
| topN                   | Integer | 0                      | yes      | Global limit on the number of scores computed. The N largest total results are returned. This value cannot be negative, a value of 0 means no global limit.
| bottomN                | Integer | 0                      | yes      | Global limit on the number of scores computed. The N smallest total results are returned. This value cannot be negative, a value of 0 means no global limit.
| writeRelationshipType  | String  | SIMILAR                | no      | The relationship type used to represent a similarity score.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type    | Description
| nodesCompared          | Integer | The number of nodes compared.
| relationshipsWritten   | Integer | The number of relationships created.
| createMillis           | Integer | Milliseconds for loading data.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| writeMillis            | Integer | Milliseconds for writing result data back to Neo4j.
| postProcessingMillis   | Integer | Milliseconds for computing percentiles.
| similarityDistribution | Map     | Map containing min, max, mean, stdDev and p1, p5, p10, p25, p75, p90, p95, p99, p100 percentile values of the computed similarity results.
| configuration          | Map     | The configuration used for running the algorithm.
|===


=== Mutate mode

.Run Node Similarity in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.nodeSimilarity.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  // general mutate return columns
  nodesCompared: Integer,
  relationshipsWritten: Integer
----

The configuration for the `mutate` mode is similar to the `write` mode.
Instead of specifying a `writeRelationshipType` and `writeProperty`,
we need to specify a `mutateRelationshipType` and `mutateProperty`.
Also, specifying `writeConcurrency` is not possible in `mutate` mode.

.The following will run the algorithm and store the results in `myGraph`:
[source, cypher]
----
CALL gds.nodeSimilarity.mutate('myGraph', {
    mutateRelationshipType: 'SIMILAR',
    mutateProperty: 'score'
})
----


[[algorithms-node-similarity-syntax-stream]]
=== Stream mode

.Run Node Similarity in stream mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.nodeSimilarity.stream(
  graphName: String,
  configuration: Map
) YIELD
  node1: Integer,
  node2: Integer,
  similarity: Float
----

include::common-parameters-named-graph.adoc[]

include::common-configuration-named-graph.adoc[]

.Run Node Similarity in stream mode on an anonymous graph.
[source, cypher]
----
CALL gds.nodeSimilarity.stream(configuration: Map)
YIELD
  node1: Integer,
  node2: Integer,
  similarity: Float
----

include::common-parameters-anonymous-graph.adoc[]

include::common-configuration-anonymous-graph.adoc[]

.Algorithm specific configuration
[opts="header",cols="1,1,1m,1,4"]
|===
| Name             | Type    | Default | Optional | Description
| similarityCutoff | Float   | 1E-42   | yes      | Lower limit for the similarity score to be present in the result. . Values must be between 0 and 1.
| degreeCutoff     | Integer | 1       | yes      | Lower limit on the node degree for a node to be considered in the comparisons. This value can not be lower than 1.
| topK             | Integer | 10      | yes      | Limit on the number of scores per node. The K largest results are returned. This value cannot be lower than 1.
| bottomK          | Integer | 10      | yes      | Limit on the number of scores per node. The K smallest results are returned. This value cannot be lower than 1.
| topN             | Integer | 0       | yes      | Global limit on the number of scores computed. The N largest total results are returned. This value cannot be negative, a value of 0 means no global limit.
| bottomN          | Integer | 0       | yes      | Global limit on the number of scores computed. The N smallest total results are returned. This value cannot be negative, a value of 0 means no global limit.
|===

.Results
[opts="header",cols="1m,1,6"]
|===
| Name       | Type    | Description
| node1      | Integer | The Neo4j ID of the first node.
| node2      | Integer | The Neo4j ID of the second node.
| similarity | Float   | The similarity score for the two nodes.
|===


=== Stats mode

.Run Node Similarity in stats mode on a named graph.
[source, cypher]
----
CALL gds.nodeSimilarity.stats(
  graphName: String,
  configuration: Map
)
YIELD
  computeMillis: Integer,
  nodesCompared: Integer,
----

include::common-parameters-named-graph.adoc[]

.Run Node Similarity in stats mode on an anonymous graph.
[source, cypher]
----
CALL gds.nodeSimilarity.stats(configuration: Map)
YIELD
  createMillis: Integer,
  computeMillis: Integer,
  nodesCompared: Integer,
----

include::common-parameters-anonymous-graph.adoc[]

The configuration is the same as for the `write` mode.

.Results
[opts="header",cols="1,1,6"]
|===
| Name                          | Type    | Description
| createMillis                  | Integer | Milliseconds for loading data.
| computeMillis                 | Integer | Milliseconds for running the algorithm.
| postProcessingMillis          | Integer | Milliseconds for computing percentiles.
| nodesCompared                 | Integer | The number of nodes compared.
| similarityDistribution        | Map     | Map containing min, max, mean, stdDev and p1, p5, p10, p25, p75, p90, p95, p99, p100 percentile values of the computed similarity results.
| configuration                 | Map     | The configuration used for running the algorithm.
|===


=== Estimate mode

The following will estimate the memory requirements for running the algorithm.
The `mode` can be substituted with the available modes (`stream`, `write` and `stats`).

.Run Node Similarity in estimate mode on a named graph.
[source,cypher]
----
CALL gds.nodeSimilarity.<mode>.estimate(
  graphName: String,
  configuration: Map
)
----

include::common-parameters-named-graph.adoc[]

.Run Node Similarity in estimate mode on an anonymous graph.
[source,cypher]
----
CALL gds.nodeSimilarity.<mode>.estimate(configuration: Map)
----

include::common-parameters-anonymous-graph.adoc[]

include::common-estimate-mode.adoc[]


[[algorithms-node-similarity-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE (alice:Person {name: 'Alice'})
CREATE (bob:Person {name: 'Bob'})
CREATE (carol:Person {name: 'Carol'})
CREATE (dave:Person {name: 'Dave'})
CREATE (eve:Person {name: 'Eve'})
CREATE (guitar:Instrument {name: 'Guitar'})
CREATE (synth:Instrument {name: 'Synthesizer'})
CREATE (bongos:Instrument {name: 'Bongos'})
CREATE (trumpet:Instrument {name: 'Trumpet'})

CREATE (alice)-[:LIKES]->(guitar)
CREATE (alice)-[:LIKES]->(synth)
CREATE (alice)-[:LIKES]->(bongos)
CREATE (bob)-[:LIKES]->(guitar)
CREATE (bob)-[:LIKES]->(synth)
CREATE (carol)-[:LIKES]->(bongos)
CREATE (dave)-[:LIKES]->(guitar)
CREATE (dave)-[:LIKES]->(synth)
CREATE (dave)-[:LIKES]->(bongos);
----

This bipartite graph has two node sets, Person nodes and Instrument nodes.
The two node sets are connected via LIKES relationships.
Each relationship starts at a Person node and ends at an Instrument node.

In the example, we want to use the Node Similarity algorithm to compare people based on the instruments they like.

The Node Similarity algorithm will only compute similarity for nodes that have a degree of at least 1.
In the example graph, the Eve node will not be compared to other Person nodes.

[NOTE]
====
In the examples below we will use named graphs and standard projections as the norm.
However, <<cypher-projection, Cypher projection>> and anonymous graphs could also be used.
====

.The following statement will create the graph and store it in the graph catalog.
[source, cypher, role=setup-query]
----
CALL gds.graph.create('myGraph', ['Person', 'Instrument'], 'LIKES');
----

Similarly, loading a graph with Cypher also requires to load the whole pool of nodes of the bipartite graph as well a the relationships that link them.

.The following statement will create a graph with Cypher and store it in the catalog.
[source, cypher, role=setup-query]
----
CALL gds.graph.create.cypher(
    'myCypherGraph',
    'MATCH (n) WHERE n:Person OR n:Instrument RETURN id(n) AS id',
    'MATCH (p:Person)-[:LIKES]->(i:Instrument) RETURN id(p) AS source, id(i) AS target'
)
----

In the following examples we will demonstrate using the Node Similarity algorithm on this graph.


[[algorithms-node-similarity-examples-stream]]
=== Streaming results

[role=query-example]
--
.The following will run the algorithm, and stream results:
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph')
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESCENDING, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Dave"  | "Alice" | 1.0
| "Alice" | "Bob"   | 0.6666666666666666
| "Bob"   | "Alice" | 0.6666666666666666
| "Bob"   | "Dave"  | 0.6666666666666666
| "Dave"  | "Bob"   | 0.6666666666666666
| "Alice" | "Carol" | 0.3333333333333333
| "Carol" | "Alice" | 0.3333333333333333
| "Carol" | "Dave"  | 0.3333333333333333
| "Dave"  | "Carol" | 0.3333333333333333
3+|10 rows
|===
--

We use default values for the procedure configuration parameter.
TopK is set to 10, topN is set to 0.
Because of that the result set contains the top 10 similarity scores for each node.


[[algorithms-node-similarity-examples-write]]
=== Writing results

To instead write the similarity results back to the graph in Neo4j, use the following query.
Each result is written as a new relationship between the compared nodes.
The Jaccard similarity score is written as a property on the relationship.

[role=query-example]
--
.The following will run the algorithm, and write back results:
[source, cypher]
----
CALL gds.nodeSimilarity.write('myGraph', {
    writeRelationshipType: 'SIMILAR',
    writeProperty: 'score'
})
YIELD nodesCompared, relationshipsWritten
----

.Results
[opts="header"]
|===
| nodesCompared | relationshipsWritten
| 4             | 10
|===
--

As we can see from the results, the number of created relationships is equal to the number of rows in the streaming example.


[[algorithms-node-similarity-examples-limits]]
=== Limiting results

There are four limits that can be applied to the similarity results.
Top limits the result to the highest similarity scores.
Bottom limits the result to the lowest similarity scores.
Both top and bottom limits can apply to the result as a whole ("N"), or to the result per node ("K").

[NOTE]
====
There must always be a "K" limit, either bottomK or topK, which is a positive number.
The default value for topK and bottomK is 10.
====


.Result limits
[opts="header", cols="1h,1,1"]
|===
|               | total results | results per node
| highest score | topN          | topK
| lowest score  | bottomN       | bottomK
|===


[[algorithms-node-similarity-examples-limits-topk-bottomk]]
==== topK and bottomK

TopK and bottomK are limits on the number of scores computed per node.
For topK, the K largest similarity scores per node are returned.
For bottomK, the K smallest similarity scores per node are returned.
TopK and bottomK cannot be 0, used in conjunction, and the default value is 10.
If neither is specified, topK is used.

[role=query-example]
--
.The following will run the algorithm, and stream the top 1 result per node:
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph', { topK: 1 })
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Bob"   | "Alice" | 0.6666666666666666
| "Carol" | "Alice" | 0.3333333333333333
| "Dave"  | "Alice" | 1.0
3+|4 rows
|===
--


[role=query-example]
--
.The following will run the algorithm, and stream the bottom 1 result per node:
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph', { bottomK: 1 })
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Carol" | 0.3333333333333333
| "Bob"   | "Alice" | 0.6666666666666666
| "Carol" | "Alice" | 0.3333333333333333
| "Dave"  | "Carol" | 0.3333333333333333
3+|4 rows
|===
--


[[algorithms-node-similarity-examples-limits-topn-bottomn]]
==== topN and bottomN

TopN and bottomN limit the number of similarity scores across all nodes.
This is a limit on the total result set, in addition to the topK or bottomK limit on the results per node.
For topN, the N largest similarity scores are returned.
For bottomN, the N smallest similarity scores are returned.
A value of 0 means no global limit is imposed and all results from topK or bottomK are returned.

[role=query-example]
--
.The following will run the algorithm, and stream the 3 highest out of the top 1 results per node:
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph', { topK: 1, topN: 3 })
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY similarity DESC, Person1, Person2
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Dave"  | "Alice" | 1.0
| "Bob"   | "Alice" | 0.6666666666666666
3+|3 rows
|===
--


[[algorithms-node-similarity-examples-degree-similarity-cutoff]]
=== Degree cutoff and similarity cutoff

Degree cutoff is a lower limit on the node degree for a node to be considered in the comparisons.
This value can not be lower than 1.

[role=query-example]
--
.The following will ignore nodes with less than 3 LIKES relationships:
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph', { degreeCutoff: 3 })
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Dave"  | "Alice" | 1.0
3+|2 rows
|===
--

Similarity cutoff is a lower limit for the similarity score to be present in the result.
The default value is very small (`1E-42`) to exclude results with a similarity score of 0.

[NOTE]
====
Setting similarity cutoff to 0 may yield a very large result set, increased runtime and memory consumption.
====

[role=query-example]
--
.The following will ignore node pairs with a similarity score less than 0.5:
[source, cypher]
----
CALL gds.nodeSimilarity.stream('myGraph', { similarityCutoff: 0.5 })
YIELD node1, node2, similarity
RETURN gds.util.asNode(node1).name AS Person1, gds.util.asNode(node2).name AS Person2, similarity
ORDER BY Person1
----

.Results
[opts="header"]
|===
| Person1 | Person2 | similarity
| "Alice" | "Dave"  | 1.0
| "Alice" | "Bob"   | 0.6666666666666666
| "Bob"   | "Dave"  | 0.6666666666666666
| "Bob"   | "Alice" | 0.6666666666666666
| "Dave"  | "Alice" | 1.0
| "Dave"  | "Bob"   | 0.6666666666666666
3+|6 rows
|===
--


[[algorithms-node-similarity-examples-memory-estimation]]
=== Memory Estimation

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.nodeSimilarity.write.estimate('myGraph', {
  writeRelationshipType: 'SIMILAR',
  writeProperty: 'score'
})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header", cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 9         | 18                | 2640     | 2640     | "2640 Bytes"
|===
--

[[algorithms-node-similarity-examples-stats]]
=== Stats

[role=query-example]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher]
----
CALL gds.nodeSimilarity.stats('myGraph')
YIELD nodesCompared
----

.Results
[opts="header",cols="1"]
|===
| nodesCompared
| 4
|===
--
