[[algorithms-bfs]]
= Breadth First Search

[abstract]
--
This section describes the Breadth First Search traversal algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-bfs-intro, Introduction>>
* <<algorithms-bfs-syntax, Syntax>>
* <<algorithms-bfs-examples, Examples>>

[[algorithms-bfs-intro]]
== Introduction

The Breadth First Search algorithm is a graph traversal algorithm that given a start node visits nodes in order of increasing distance, see https://en.wikipedia.org/wiki/Breadth-first_search.
A related algorithm is the Depth First Search algorithm, <<algorithms-dfs, Depth First Search>>.
This algorithm is useful for searching when the likelihood for finding the node searched for decreases with distance.
There are multiple termination conditions supported for the traversal, based on either reaching one of several target nodes, reaching a maximum depth, exhausting a given budget of traversed relationship cost, or just traversing the whole graph.
The output of the procedure contains information about what nodes were visited and in what order.

[[algorithms-bfs-syntax]]
== Syntax

.The following describes the API for running the algorithm and stream results:
[source, cypher]
----
CALL gds.alpha.bfs.stream(
  graphName: string,
  configuration: map
)
YIELD
  // general stream return columns
  startNodeId: int,
  nodeIds: int,
  path: Path
----

.Configuration
[opts="header",cols="1,1,1,1,4"]
|===
| Name                   | Type         | Default                | Optional | Description
| nodeProjection         | string       | null                   | yes      | The projection of nodes to use when creating the implicit graph.
| relationshipProjection | string       | null                   | yes      | The projection of relationships to use when creating the implicit graph.
| concurrency            | int          | 4                      | yes      | The number of concurrent threads used for running the algorithm. Also provides the default value for 'readConcurrency' and 'writeConcurrency'. This is dependent on the Neo4j edition; for more information, see <<system-requirements-cpu>>.
| readConcurrency        | int          | value of 'concurrency' | yes      | The number of concurrent threads used for reading the graph.
| startNodeId            | int          | n/a                    | no       | The node id of the node where to start the traversal.
| targetNodes            | list of int  | empty list             | yes      | Ids for target nodes. Traversal terminates when any target node is visited.
| maxDepth               | int          | -1                     | yes      | The maximum distance from the start node at which nodes are visited.
| maxCost                | int          | NaN                    | yes      | The maximum accumulated cost of any path from start node to a node that should be visited.
|===

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type        | Description
| startNodeId   | int         | The node id of the node where to start the traversal.
| nodeIds       | list of int | The ids of all nodes that were visited during the traversal.
| path          | path        | A path containing all the nodes that were visited during the traversal.
|===


[[algorithms-bfs-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher]
----
CREATE
       (nA:Node {tag: 'a'}),
       (nB:Node {tag: 'b'}),
       (nC:Node {tag: 'c'}),
       (nD:Node {tag: 'd'}),
       (nE:Node {tag: 'e'}),

       (nA)-[:REL {cost: 8.0}]->(nB),
       (nA)-[:REL {cost: 9.0}]->(nC),
       (nB)-[:REL {cost: 1.0}]->(nE),
       (nC)-[:REL {cost: 5.0}]->(nD)
----

.The following statement will create the graph and store it in the graph catalog.
[source, cypher]
----
CALL gds.graph.create('myGraph', 'Node', 'REL', { relationshipProperties: 'cost' })
----

In the following examples we will demonstrate using the Breadth First Search algorithm on this graph.

.Running the Breadth First Search algorithm:
[source, cypher]
----
MATCH (a:Node{tag:'a'})
WITH id(a) as startNode
CALL gds.alpha.bfs.stream('myGraph', {startNode: startNode})
YIELD path
UNWIND [ n in nodes(path) | n.tag ] as tags
RETURN tags
ORDER BY tags
----

.Results
[opts="header",cols="1m"]
|===
| tags
| a
| b
| c
| d
| e
|===

Since none of the options for early termination are specified, the whole graph is visited during the traversal.

.Running the Breadth First Search algorithm with target nodes:
[source, cypher]
----
MATCH (a:Node{tag:'a'}), (d:Node{tag:'d'}), (e:Node{tag:'e'})
WITH id(a) as startNode, [id(d), id(e)] as targetNodes
CALL gds.alpha.bfs.stream('myGraph', {startNode: startNode, targetNodes: targetNodes})
YIELD path
UNWIND [ n in nodes(path) | n.tag ] as tags
RETURN tags
ORDER BY tags
----

.Results
[opts="header",cols="1m"]
|===
| tags
| a
| b
| c
| e
|===

.Running the Breadth First Search algorithm with maxDepth:
[source, cypher]
----
MATCH (a:Node{tag:'a'})
WITH id(a) as startNode
CALL gds.alpha.bfs.stream('myGraph', {startNode: startNode, maxDepth: 1})
YIELD path
UNWIND [ n in nodes(path) | n.tag ] as tags
RETURN tags
ORDER BY tag
----

.Results
[opts="header",cols="1m"]
|===
| tags
| a
| b
| c
|===

In the above example, nodes d and e were not visited since they are at distance 2 from a.

.Running the Breadth First Search algorithm with maxCost:
[source, cypher]
----
MATCH (a:Node{tag:'a'})
WITH id(a) as startNode
CALL gds.alpha.bfs.stream('myGraph', {startNode: startNode, maxCost: 10, relationshipWeightProperty: 'cost'})
YIELD path
UNWIND [ n in nodes(path) | n.tag ] as tags
RETURN tags
ORDER BY tag
----

.Results
[opts="header",cols="1m"]
|===
| tags
| a
| b
| c
| e
|===

Due to the max cost limit of 10, node d cannot be reached since the total cost of the path from a to d is 14.
