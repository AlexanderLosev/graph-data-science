[[algorithms-embeddings-fastrp]]
= Fast Random Projection

[abstract]
--
This section describes the Fast Random Projection (FastRP) node embedding algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-embeddings-fastrp-introduction>>

* <<algorithms-embeddings-fastrp-syntax, Syntax>>
** <<algorithms-embeddings-fastrp-syntax-anonymous, Anonymous graphs>>
* <<algorithms-embeddings-fastrp-examples, Examples>>
** <<algorithms-embeddings-fastrp-examples-memory-estimation, Memory Estimation>>
** <<algorithms-embeddings-fastrp-examples-stream, Stream>>
** <<algorithms-embeddings-fastrp-examples-stats, Stats>>
** <<algorithms-embeddings-fastrp-examples-mutate, Mutate>>
** <<algorithms-embeddings-fastrp-examples-write, Write>>
** <<algorithms-embeddings-fastrp-examples-weighted, Weighted>>
* <<algorithms-embeddings-fastrp-parameter-tuning, Hyperparameter tuning>>


[[algorithms-embeddings-fastrp-introduction]]
== Introduction

Fast Random Projection, or FastRP for short, is a node embedding algorithm in the family of random projection algorithms.
These algorithms are theoretically backed by the Johnsson-Lindenstrauss lemma according to which, one can project _n_ vectors of _arbitrary_ dimension into _O(log(n))_ dimensions and still approximately preserve pairwise distances among the points.
In fact, a linear projection chosen in a random way satisfies this property.

Such techniques therefore allow for aggressive dimensionality reduction while preserving most of the distance information.
The FastRP algorithm operates on graphs, in which case we care about preserving similarity between nodes and their neighbors.
This means that two nodes that have similar neighborhoods should be assigned similar embedding vectors.
Conversely, two nodes that are not similar should be not be assigned similar embedding vectors.

The FastRP algorithm initially assigns random vectors to all nodes using a technique called _very sparse random projection_, see (Achlioptas, 2003) below.
The algorithm then iteratively constructs _intermediate_ embeddings by averaging neighboring intermediate embeddings from the previous iteration, or the generated random vectors during the first iteration.

In the end, the resulting embedding for each node is a weighted sum of the intermediate embeddings, where the weights are a configuration parameter called `iterationWeights`.

Therefore, each node's embedding depends on a neighborhood of radius equal to the number of iterations.
This way FastRP exploits higher-order relationships in the graph while still being highly scalable.

The present implementation extends the original algorithm to support weighted graphs, which computes weighted averages of neighboring embeddings using the relationship weights.
In order to make use of this, the `relationshipWeightProperty` parameter should be set to an existing relationship property.

The original algorithm is intended only for undirected graphs.
We support running on both on directed graphs and undirected graph.
For directed graphs we consider only the outgoing neighbors when computing the intermediate embeddings for a node.
Therefore, using the orientations NATURAL, REVERSE and UNDIRECTED will all give different embeddings.
In general, it is recommended to first use UNDIRECTED as this is what the original algorithm was evaluated on.

For more information on this algorithm see:

* https://arxiv.org/pdf/1908.11512.pdf[H. Chen, S.F. Sultan, Y. Tian, M. Chen, S. Skiena: Fast and Accurate Network Embeddings via Very Sparse Random Projection, 2019.^]
* https://core.ac.uk/download/pdf/82724427.pdf[Dimitris Achlioptas. Database-friendly random projections: Johnson-Lindenstrauss with binary coins. Journal of Computer and System Sciences, 66(4):671â€“687, 2003.]


[[algorithms-embeddings-fastrp-syntax]]
== Syntax

.FastRP syntax per mode
[.tabbed-example]
====

[.include-with-stream]
======
.Run FastRP in stream mode on a named graph.
[source, cypher]
----
CALL gds.fastRP.stream(
  graphName: String,
  configuration: Map
) YIELD
  nodeId: Integer,
  embedding: List<Float>
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1m,1,6"]
|===
| Name      | Type         | Description
| nodeId    | Integer      | The Neo4j node ID.
| embedding | List<Float>  | The computed node embedding.
|===
======

[.include-with-write]
======
.Run FastRP in write mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.fastRP.write(
  graphName: String,
  configuration: Map
)
YIELD
  nodeCount: Integer,
  propertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name               | Type                 | Description
| nodeCount          | Integer              | The number of nodes processed.
| propertiesWritten  | Integer              | The number of node properties written.
| createMillis       | Integer              | Milliseconds for loading data.
| computeMillis      | Integer              | Milliseconds for running the algorithm.
| writeMillis        | Integer              | Milliseconds for writing result data back to Neo4j.
| configuration      | Map                  | The configuration used for running the algorithm.
|===
======
====


[[algorithms-embeddings-fastrp-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run FastRP in write mode on an anonymous graph.
[source, cypher]
----
CALL gds.fastRP.write(
  configuration: Map
)
YIELD
  nodeCount: Integer,
  propertiesWritten: Integer,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, see the <<algorithms-embeddings-fastrp-syntax, write mode syntax above>>.




[[algorithms-embeddings-fastrp-examples]]
== Examples

Consider the graph created by the following Cypher statement:

[source, cypher, role=setup-query]
----
CREATE
  (Dan:Person),
  (Annie:Person),
  (Matt:Person),
  (Jeff:Person),
  (Brie:Person),
  (Elsa:Person),
  (John:Person),

  (Dan)-[:REL {weight: 1.0}]->(Annie),
  (Dan)-[:REL {weight: 1.0}]->(Matt),
  (Annie)-[:REL {weight: 1.0}]->(Matt),
  (Annie)-[:REL {weight: 1.0}]->(Jeff),
  (Annie)-[:REL {weight: 1.0}]->(Brie),
  (Matt)-[:REL {weight: 3.5}]->(Brie),
  (Brie)-[:REL {weight: 1.0}]->(Elsa),
  (Brie)-[:REL {weight: 2.0}]->(Jeff),
  (John)-[:REL {weight: 1.0}]->(Jeff);
----

[source, cypher, role=graph-create-query]
----
CALL gds.graph.create(
  'persons',
  'Person',
  {
    REL: {
      orientation: 'UNDIRECTED',
        properties: {
           weight: {
             property: 'weight',
             defaultValue: 1.0
           }
        }
    }
})
----


[[algorithms-embeddings-fastrp-examples-memory-estimation]]
=== Memory Estimation

:mode: stream
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.fastRP.stream.estimate('persons', {embeddingDimension: 128})
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header", cols="1,1,1,1,1"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 18                | 11392    | 11392    | "11392 Bytes"
|===
--


[[algorithms-embeddings-fastrp-examples-stream]]
=== Stream

[role=query-example, no-result=true]
--
.The following will run the algorithm, and stream results:
[source, cypher]
----
CALL gds.fastRP.stream(
  'persons',
  {
    iterationWeights: [0.0, 0.34, 2.1],
    embeddingDimension: 3,
    normalizationStrength: -0.71
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [-1.9220695495605469,0.8281561136245728,-0.23398679494857788]
| 1      | [-0.9239899516105652,1.9944313764572144,-0.5190169811248779]
| 2      | [-0.8023543953895569,2.028059959411621,0.4712531268596649]
| 3      | [-1.2199127674102783,1.1800869703292847,1.1962001323699951]
| 4      | [-1.746078610420227,0.5176445245742798,-0.9333235621452332]
| 5      | [0.0782150849699974,1.6632782220840454,0.7139096260070801]
| 6      | [-0.8536702990531921,1.0019549131393433,-1.374551773071289]
|===
--

[NOTE]
====
Due to the random nature of the algorithm the results will vary between the runs.
However, this does not necessarily mean that the pairwise distances of two node embeddings vary as much.
====


[[algorithms-embeddings-fastrp-examples-stats]]
=== Stats

[role=query-example, no-result=true]
--
.The following will run the algorithm and returns the result in form of statistical and measurement values
[source, cypher]
----
CALL gds.fastRP.stats(
  'persons',
  {
    iterationWeights: [0.0, 0.34, 2.1],
    embeddingDimension: 3,
    normalizationStrength: -0.71
  }
)
YIELD createMillis, computeMillis
----

[opts=header]
.Results
|===
| createMillis | computeMillis
| 0            | 5
|===
--


[[algorithms-embeddings-fastrp-examples-mutate]]
=== Mutate

[role=query-example, no-result=true]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher]
----
CALL gds.fastRP.mutate(
  'persons',
  {
    iterationWeights: [0.0, 0.34, 2.1],
    embeddingDimension: 3,
    normalizationStrength: -0.71,
    mutateProperty: 'embedding'
  }
)
YIELD nodePropertiesWritten, mutateMillis

----

[opts=header]
.Results
|===
| nodePropertiesWritten | mutateMillis
| 7                     | 0
|===
--


[[algorithms-embeddings-fastrp-examples-write]]
=== Write

[role=query-example, no-result=true]
--
.The following will run the algorithm in `write` mode:
[source, cypher]
----
CALL gds.fastRP.write(
  'persons',
  {
    iterationWeights: [0.0, 0.34, 2.1],
    embeddingDimension: 3,
    normalizationStrength: -0.71,
    writeProperty: 'embedding'
  }
)
YIELD nodePropertiesWritten, writeMillis

----

[opts=header]
.Results
|===
| nodePropertiesWritten | mutateMillis
| 7                     | 212
|===
--


[[algorithms-embeddings-fastrp-examples-weighted]]
=== Weighted

[role=query-example, no-result=true]
--
.The following will run the algorithm, and stream results:
[source, cypher]
----
CALL gds.fastRP.stream(
  'persons',
  {
    iterationWeights: [0.0, 0.34, 2.1],
    embeddingDimension: 3,
    normalizationStrength: -0.71,
    relationshipWeightProperty: 'weight'
  }
)
YIELD nodeId, embedding
----

[opts=header]
.Results
|===
| nodeId | embedding
| 0      | [-1.9220695495605469,0.8281561136245728,-0.23398679494857788]
| 1      | [-0.9239899516105652,1.9944313764572144,-0.5190169811248779]
| 2      | [-0.8023543953895569,2.028059959411621,0.4712531268596649]
| 3      | [-1.2199127674102783,1.1800869703292847,1.1962001323699951]
| 4      | [-1.746078610420227,0.5176445245742798,-0.9333235621452332]
| 5      | [0.0782150849699974,1.6632782220840454,0.7139096260070801]
| 6      | [-0.8536702990531921,1.0019549131393433,-1.374551773071289]
|===
--


[[algorithms-embeddings-fastrp-parameter-tuning]]
== Tuning algorithm parameters

In order to improve the embedding quality using FastRP on one of your graphs, it is possible to tune the algorithm parameters.
This process of finding the best parameters for your specific use case and graph is typically referred to as https://en.wikipedia.org/wiki/Hyperparameter_optimization[hyperparameter tuning].
We will go through each of the configuration parameters and explain how they behave.

For statistically sound results, it is a good idea to reserve a test set excluded from parameter tuning.
After hyperparameter tuning has been made, the embedding with the best settings can then be evaluated using a downstream machine learning task on the test set.
To construct such a set you may want to create a node label in the graph using cypher that represents a subgraph without the test data.

=== Embedding Dimension

The optimal value for `embeddingDimension` depends on the number of nodes in the graph.
Since the amount of information the embedding can encode is limited by the `embeddingDimension`, a larger graph will tend to require a higher embedding dimension.
A typical value is a power of two in the range 128 - 1024.
A value of at least 256 gives good results on graphs with as many as 200000 nodes, but in general increasing the dimension improves results.
Increasing embedding dimension will however increase memory requirements and runtime linearly.

=== Normalization strength

The normalization strength is used to control how node degree influences the embedding.
Using a negative value will downplay the importance of high degree neighbors, while a positive value will instead increase their importance.
The optimal normalization strength depends on the graph and on the task that the embeddings will be used for.
In the original paper, hyperparameter tuning was done on the range `[-1,0]`, but we have found a case where slightly positive normalization strength gave better results.

=== Iteration weights

By selecting `iterationWeights`, it is possible both to set the number of iterations and the weight given to the embedding resulting from each iteration.
The embedding corresponding to the `i`:th iteration contains features depending on nodes reachable with paths of length `i`.
If the graph is undirected, then a node reachable with a path of length `L` can also be reached with length `L+2k`, for any integer `k`.
In particular, the third iteration will in this case contain features depending both on direct neighbors and nodes at distance three.
It is good to have at least one non-zero weight in an even and in an odd position.
Typically, using at least a few iterations, for example three, is recommended.
However, a too high value will consider nodes far away and may not be informative or even be detrimental.
The intuition here is that as the projections reach further away from the node, the less specific the neighborhood becomes.


=== Orientation
Choosing the right orientation when creating the graph may have the single biggest impact.
The FastRP algorithm is designed only to work with undirected graphs, and we expect this to be the best in most cases.
If you expect only outgoing or incoming relationships to be informative for a prediction task, then you may want to try using the orientations `NATURAL` or `REVERSE` respectively.


