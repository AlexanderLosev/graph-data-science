[[algorithms-betweenness-centrality]]
= Betweenness Centrality
:entity: node
:result: centrality
:algorithm: Betweenness Centrality

[abstract]
--
This section describes the Betweenness Centrality algorithm in the Neo4j Graph Data Science library.
--

This topic includes:

* <<algorithms-betweenness-centrality-intro, Introduction>>
* <<algorithms-betweenness-centrality-considerations, Considerations and sampling>>
* <<algorithms-betweenness-centrality-syntax, Syntax>>
** <<algorithms-betweenness-centrality-syntax-stream, Stream>>
** <<algorithms-betweenness-centrality-syntax-stats, Stats>>
** <<algorithms-betweenness-centrality-syntax-mutate, Mutate>>
** <<algorithms-betweenness-centrality-syntax-write, Write>>
** <<algorithms-betweenness-centrality-syntax-anonymous, Anonymous graphs>>
* <<algorithms-betweenness-centrality-examples, Examples>>
** <<algorithms-betweenness-centrality-examples-memory-estimation, Memory Estimation>>
** <<algorithms-betweenness-centrality-examples-stream, Stream>>
** <<algorithms-betweenness-centrality-examples-stats, Stats>>
** <<algorithms-betweenness-centrality-examples-mutate, Mutate>>
** <<algorithms-betweenness-centrality-examples-write, Write>>
** <<algorithms-betweenness-centrality-examples-sampling, Sampling>>
** <<algorithms-betweenness-centrality-examples-undirected, Undirected>>


[[algorithms-betweenness-centrality-intro]]
== Introduction

Betweenness centrality is a way of detecting the amount of influence a node has over the flow of information in a graph.
It is often used to find nodes that serve as a bridge from one part of a graph to another.

The algorithm calculates the shortest paths between all pairs of nodes in an unweighted graph.
Each node receives a score, based on the number of shortest paths that pass through the node.
Nodes that more frequently lie on shortest paths between other nodes will have higher betweenness centrality scores.

The GDS implementation is based on https://www.uni-konstanz.de/mmsp/pubsys/publishedFiles/BrPi07.pdf[Brandes' approximate algorithm^] for unweighted graphs.
The implementation requires _O(n + m)_ space and runs in _O(n * m)_ time, where _n_ is the number of nodes and _m_ the number of relationships in the graph.

For more information on this algorithm, see:

* https://www.eecs.wsu.edu/~assefaw/CptS580-06/papers/brandes01centrality.pdf[A Faster Algorithm for Betweenness Centrality^]
* https://www.uni-konstanz.de/mmsp/pubsys/publishedFiles/BrPi07.pdf[Centrality Estimation in Large Networks^]
* http://moreno.ss.uci.edu/23.pdf[A Set of Measures of Centrality Based on Betweenness^]

[NOTE]
====
Running this algorithm requires sufficient memory availability.
Before running this algorithm, we recommend that you read <<memory-estimation>>.
====


[[algorithms-betweenness-centrality-considerations]]
== Considerations and sampling

The Betweenness Centrality algorithm can be very resource-intensive to compute.
https://www.uni-konstanz.de/mmsp/pubsys/publishedFiles/BrPi07.pdf[Brandes' approximate algorithm^] computes single-source shortest paths (SSSP) for a set of source nodes.
When all nodes are selected as source nodes, the algorithm produces an exact result.
However, for large graphs this can potentially lead to very long runtimes.
Thus, approximating the results by computing the SSSPs for only a subset of nodes can be useful.
In GDS we refer to this technique as _sampling_, where the size of the source node set is the _sampling size_.

There are two things to consider when executing the algorithm on large graphs:

* A higher parallelism leads to higher memory consumption as each thread executes SSSPs for a subset of source nodes sequentially.
** In the worst case, a single SSSP requires the whole graph to be duplicated in memory.
* A higher sampling size leads to more accurate results, but also to a potentially much longer execution time.

Changing the values of the configuration parameters `concurrency` and `samplingSize`, respectively, can help to manage these considerations.


[[algorithms-betweenness-centrality-sampling-strategies]]
=== Sampling strategies

Brandes defines several strategies for selecting source nodes.
The GDS implementation is based on the random degree selection strategy, which selects nodes with a probability proportional to their degree.
The idea behind this strategy is that such nodes are likely to lie on many shortest paths in the graph and thus have a higher contribution to the betweenness centrality score.


[[algorithms-betweenness-centrality-syntax]]
== Syntax

include::../shared/syntax-intro-named-graph.adoc[]

[[algorithms-betweenness-centrality-syntax-stream]]
=== Stream

.Run Betweenness Centrality in stream mode on a named graph.
[source, cypher]
----
CALL gds.betweenness.stream(
  graphName: String,
  configuration: Map
)
YIELD
  nodeId: Integer,
  score: Float
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header"]
|===
| Name   | Type    | Description
| nodeId | Integer | Node ID.
| score  | Float   | Betweenness Centrality score.
|===


[[algorithms-betweenness-centrality-syntax-stats]]
=== Stats

.Run Betweenness Centrality in stats mode on a named graph.
[source, cypher]
----
CALL gds.betweenness.stats(
  graphName: String,
  configuration: Map
)
YIELD
  minCentrality: Float,
  maxCentrality: Float,
  sumCentrality: Float,
  createMillis: Integer,
  computeMillis: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-stream-stats-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name          | Type    | Description
| minCentrality | Float   | Lowest centrality score.
| maxCentrality | Float   | Greatest centrality score.
| sumCentrality | Float   | Sum of all centrality scores.
| createMillis  | Integer | Milliseconds for creating the graph.
| computeMillis | Integer | Milliseconds for running the algorithm.
| configuration | Map     | Configuration used for running the algorithm.
|===


[[algorithms-betweenness-centrality-syntax-mutate]]
=== Mutate

.Run Betweenness Centrality in mutate mode on a graph stored in the catalog.
[source, cypher]
----
CALL gds.betweenness.mutate(
  graphName: String,
  configuration: Map
)
YIELD
  minCentrality: Float,
  maxCentrality: Float,
  sumCentrality: Float,
  createMillis: Integer,
  computeMillis: Integer,
  mutateMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-mutate-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                  | Type    | Description
| minCentrality         | Float   | Lowest centrality score.
| maxCentrality         | Float   | Greatest centrality score.
| sumCentrality         | Float   | Sum of all centrality scores.
| createMillis          | Integer | Milliseconds for creating the graph.
| computeMillis         | Integer | Milliseconds for running the algorithm.
| mutateMillis          | Integer | Milliseconds for adding properties to the in-memory graph.
| nodePropertiesWritten | Integer | Number of properties added to the in-memory graph.
| configuration         | Map     | Configuration used for running the algorithm.
|===


[[algorithms-betweenness-centrality-syntax-write]]
=== Write

.Run Betweenness Centrality in write mode on a named graph.
[source, cypher]
----
CALL gds.betweenness.write(
  graphName: String,
  configuration: Map
)
YIELD
  minCentrality: Float,
  maxCentrality: Float,
  sumCentrality: Float,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../common-configuration/common-parameters-named-graph.adoc[]

include::../common-configuration/common-write-configuration-named-graph.adoc[]

include::specific-configuration.adoc[]

.Results
[opts="header",cols="1,1,6"]
|===
| Name                   | Type    | Description
| minCentrality          | Float   | Lowest centrality score.
| maxCentrality          | Float   | Highest centrality score.
| sumCentrality          | Float   | Sum of all centrality scores.
| createMillis           | Integer | Milliseconds for creating the graph.
| computeMillis          | Integer | Milliseconds for running the algorithm.
| writeMillis            | Integer | Milliseconds for writing result data back.
| nodePropertiesWritten  | Integer | Number of properties written to Neo4j.
| configuration          | Map     | The configuration used for running the algorithm.
|===


[[algorithms-betweenness-centrality-syntax-anonymous]]
=== Anonymous graphs

include::../shared/syntax-anonymous-graphs.adoc[]

.Run Betweenness Centrality in write mode on an anonymous graph:
[source, cypher]
----
CALL gds.betweenness.write(
  configuration: Map
)
YIELD
  minCentrality: Float,
  maxCentrality: Float,
  sumCentrality: Float,
  createMillis: Integer,
  computeMillis: Integer,
  writeMillis: Integer,
  nodePropertiesWritten: Integer,
  configuration: Map
----

include::../common-configuration/common-write-configuration-anonymous-graph.adoc[]

include::specific-configuration.adoc[]

The results are the same as for running write mode with a named graph, <<algorithms-betweenness-centrality-syntax-write, specified above>>.


[[algorithms-betweenness-centrality-examples]]
== Examples

In this section we will show examples of executing the Betweenness Centrality algorithm on a concrete graph.
The intention is to illustrate what the results look like and to provide a guide in how to make use of the algorithm in a real setting.
We will do this on a small social network graph of a handful nodes connected in a particular pattern.
The example graph looks like this:

[[algorithms-betweenness-centrality-examples-graph]]
image::betweenness_centrality.png[]

.The following Cypher statement will create the example graph in the Neo4j database:
[source, cypher, role=setup-query]
----
CREATE
  (alice:User {name: 'Alice'}),
  (bob:User {name: 'Bob'}),
  (carol:User {name: 'Carol'}),
  (dan:User {name: 'Dan'}),
  (eve:User {name: 'Eve'}),
  (frank:User {name: 'Frank'}),
  (gale:User {name: 'Gale'}),

  (alice)-[:FOLLOWS]->(carol),
  (bob)-[:FOLLOWS]->(carol),
  (carol)-[:FOLLOWS]->(dan),
  (carol)-[:FOLLOWS]->(eve),
  (dan)-[:FOLLOWS]->(frank),
  (eve)-[:FOLLOWS]->(frank),
  (frank)-[:FOLLOWS]->(gale);
----

With the graph in Neo4j we can now project it into the graph catalog to prepare it for algorithm execution.
We do this using a native projection targeting the `User` nodes and the `FOLLOWS` relationships.

include::../shared/examples-named-native-note.adoc[]

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'myGraph'.
[source, cypher, role=graph-create-query]
----
CALL gds.graph.create('myGraph', 'User', 'FOLLOWS')
----

In the following examples we will demonstrate using the Betweenness Centrality algorithm on this graph.


[[algorithms-betweenness-centrality-examples-memory-estimation]]
=== Memory Estimation

:mode: write
include::../shared/examples-estimate-intro.adoc[]

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.betweenness.write.estimate('myGraph', { writeProperty: 'betweenness' })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 7                 | 2904     | 2904     | "2904 Bytes"
|===
--

As is discussed in <<algorithms-betweenness-centrality-considerations>> we can configure the memory requirements using the `concurrency` configuration parameter.

[role=query-example]
--
.The following will estimate the memory requirements for running the algorithm:
[source, cypher]
----
CALL gds.betweenness.write.estimate('myGraph', { writeProperty: 'betweenness', concurrency: 1 })
YIELD nodeCount, relationshipCount, bytesMin, bytesMax, requiredMemory
----

.Results
[opts="header"]
|===
| nodeCount | relationshipCount | bytesMin | bytesMax | requiredMemory
| 7         | 7                 | 840      | 840      | "840 Bytes"
|===
--

Here we can note that the estimated memory requirements were lower than when running with the default concurrency setting.
Similarly, using a higher value will increase the estimated memory requirements.


[[algorithms-betweenness-centrality-examples-stream]]
=== Stream

:stream-details: For example, we can order the results to find the nodes with the highest betweenness centrality.
include::../shared/examples-stream-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stream` mode:
[source, cypher]
----
CALL gds.betweenness.stream('myGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name    | score
| "Alice" | 0.0
| "Bob"   | 0.0
| "Carol" | 8.0
| "Dan"   | 3.0
| "Eve"   | 3.0
| "Frank" | 5.0
| "Gale"  | 0.0
|===
--

We note that the 'Carol' node has the highest score, followed by the 'Frank' node.
Studying the <<algorithms-betweenness-centrality-examples-graph, example graph>> we can see that these nodes are in bottleneck positions in the graph.
The 'Carol' node connects the 'Alice' and 'Bob' nodes to all other nodes, which increases its score.
In particular, the shortest path from 'Alice' or 'Bob' to any other reachable node passes through 'Carol'.
Similarly, all shortest paths that lead to the 'Gale' node passes through the 'Frank' node.
Since 'Gale' is reachable from each other node, this causes the score for 'Frank' to be high.

Conversely, there are no shortest paths that pass through either of the nodes 'Alice', 'Bob' or 'Gale' which causes their betweenness centrality score to be zero.


[[algorithms-betweenness-centrality-examples-stats]]
=== Stats

:stats-details: In particular, Betweenness Centrality returns the minimum, maximum and sum of all centrality scores.
:stats-syntax: algorithms-betweenness-centrality-syntax-stats
include::../shared/examples-stats-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `stats` mode:
[source, cypher]
----
CALL gds.betweenness.stats('myGraph')
YIELD minCentrality, maxCentrality, sumCentrality
----

.Results
[opts="header"]
|===
| minCentrality | maxCentrality | sumCentrality
| 0.0           | 8.0           | 19.0
|===
--

Comparing this to the results we saw in the <<algorithms-betweenness-centrality-examples-stream, stream example>>, we can find our minimum and maximum values from the table.
It is worth noting that unless the graph has a particular shape involving a directed cycle, the `minCentrality` value will almost always be zero.
The `sumCentrality` value can be used to compute an average centrality value by dividing over the number of nodes in the graph.


[[algorithms-betweenness-centrality-examples-mutate]]
=== Mutate

include::../shared/examples-mutate-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `mutate` mode:
[source, cypher]
----
CALL gds.betweenness.mutate('myGraph', { mutateProperty: 'betweenness' })
YIELD minCentrality, maxCentrality, sumCentrality, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minCentrality | maxCentrality | sumCentrality | nodePropertiesWritten
| 0.0           | 8.0           | 19.0          | 7
|===
--

The returned result is the same as in the `stats` example.
Additionally, the graph 'myGraph' now has a node property `betweenness` which stores the betweenness centrality score for each node.
To find out how to inspect the new schema of the in-memory graph, see <<catalog-graph-list>>.


[[algorithms-betweenness-centrality-examples-write]]
=== Write

include::../shared/examples-write-intro.adoc[]

[role=query-example]
--
.The following will run the algorithm in `write` mode:
[source, cypher]
----
CALL gds.betweenness.write('myGraph', { writeProperty: 'betweenness' })
YIELD minCentrality, maxCentrality, sumCentrality, nodePropertiesWritten
----

.Results
[opts="header"]
|===
| minCentrality | maxCentrality | sumCentrality | nodePropertiesWritten
| 0.0           | 8.0           | 19.0          | 7
|===
--

The returned result is the same as in the `stats` example.
Additionally, each of the seven nodes now has a new property `betweenness` in the Neo4j database, containing the betweenness centrality score for that node.


[[algorithms-betweenness-centrality-examples-sampling]]
=== Sampling

Betweenness Centrality can be very resource-intensive to compute.
To help with this, it is possible to approximate the results using a sampling technique.
The configuration parameters `samplingSize` and `samplingSeed` are used to control the sampling.
We illustrate this on our example graph by approximating Betweenness Centrality with a sampling size of two.
The seed value is an arbitrary integer, where using the same value will yield the same results between different runs of the procedure.

[role=query-example]
--
.The following will run the algorithm in `stream` mode with a sampling size of two:
[source, cypher]
----
CALL gds.betweenness.stream('myGraph', {samplingSize: 2, samplingSeed: 0})
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name    | score
| "Alice" | 0.0
| "Bob"   | 0.0
| "Carol" | 4.0
| "Dan"   | 2.0
| "Eve"   | 2.0
| "Frank" | 3.0
| "Gale"  | 0.0
|===
--

Here we can see that the 'Carol' node has the highest score, followed by the 'Frank' node.
This is consistent with the exact results, which we studied in the <<algorithms-betweenness-centrality-examples-stream, stream example>>.
In fact, setting the `samplingSize` to the node count of the graph (seven, in our case) will produce exact results.

However, already at sampling size two, our approximate results are good enough to show which nodes were the most central.
Furthermore, the two nodes that we did select for computation seems to have been either of 'Alice' and 'Bob' as well as 'Carol'.
Picking 'Carol' is indeed very likely, because that node has the maximum outgoing degree in the graph.


[[algorithms-betweenness-centrality-examples-undirected]]
=== Undirected

Betweenness Centrality can also be run on undirected graphs.
To illustrate this, we will project our example graph using the `UNDIRECTED` orientation.

.The following statement will create a graph using a native projection and store it in the graph catalog under the name 'myUndirectedGraph'.
[source, cypher, role=graph-create-query]
----
CALL gds.graph.create('myUndirectedGraph', 'User', {FOLLOWS: {orientation: 'UNDIRECTED'}})
----

Now we can run Betweenness Centrality on our undirected graph.
The algorithm automatically figures out that the graph is undirected.


[WARNING]
Running the algorithm on an undirected graph is about twice as computationally intensive compared to a directed graph.

[role=query-example]
--
.The following will run the algorithm in `stream` mode on the undirected graph:
[source, cypher]
----
CALL gds.betweenness.stream('myUndirectedGraph')
YIELD nodeId, score
RETURN gds.util.asNode(nodeId).name AS name, score
ORDER BY name ASC
----

.Results
[opts="header"]
|===
| name    | score
| "Alice" | 0.0
| "Bob"   | 0.0
| "Carol" | 9.5
| "Dan"   | 3.0
| "Eve"   | 3.0
| "Frank" | 5.5
| "Gale"  | 0.0
|===
--

The central nodes now have slightly higher scores, due to the fact that there are more shortest paths in the graph, and these are more likely to pass through the central nodes.
The 'Dan' and 'Eve' nodes retain the same centrality scores as in the directed case.
