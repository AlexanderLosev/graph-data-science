pluginManagement {
    repositories {
        maven {
            url 'https://neo.jfrog.io/neo/docs-maven'
        }
        gradlePluginPortal()
    }
}

ext {
    neo4jMajorVersion = properties.getOrDefault('neo4jVersion', '3.5')
    compatibilityModules = [
            "cypher-printer",
            "neo4j-adapter",
            "neo4j-export-adapter",
            "neo4j-collections",
            "test-utils-adapter"
    ]
}

rootProject.name = 'graph-data-science'

def registerSubModules(File file) {
    def moduleDirectories = new LinkedList<File>()

    file.eachFileRecurse {f ->
        if (f.name == "build.gradle") {
            moduleDirectories.add(f.parentFile)
        }
    }

    moduleDirectories.stream()
            .filter { moduleDir -> moduleDir != new File("./") && moduleDir != new File("./public") }
            .filter { moduleDir ->
                def parent = moduleDir.getParentFile();
                while (parent != null) {
                    if (parent.getName() == "examples") {
                        return false;
                    }
                    parent = parent.getParentFile();
                }
                return true;
            }
            .filter { moduleDir ->
                if (compatibilityModules.contains(moduleDir.getName())) {
                    moduleDir.getParentFile().name == neo4jMajorVersion
                } else {
                    true
                }
            }
            .forEach { moduleDir ->
                include(moduleDir.name)
                project(":${moduleDir.name}").projectDir = moduleDir
            }
}

registerSubModules(new File("./"))