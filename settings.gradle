pluginManagement {
    repositories {
        maven {
            url 'https://neo.jfrog.io/neo/docs-maven'
        }
        gradlePluginPortal()
    }
}

ext {
    neo4jMajorVersion = properties.getOrDefault('neo4jVersion', '4.0').toString().split('\\.')[0..1].join('.')
    compatibilityModules = [
            "cypher-printer",
            "neo4j-adapter",
            "test-utils-adapter"
    ]
}

rootProject.name = 'graph-data-science'

def registerSubModules(File file) {
    def moduleDirectories = new LinkedList<File>()

    file.eachFileRecurse {f ->
        if (f.name == "build.gradle") {
            moduleDirectories.add(f.parentFile)
        }
    }

    moduleDirectories.stream()
            .filter { moduleDir -> moduleDir != new File("./") && moduleDir != new File("./public") }
            .filter { moduleDir ->
                def parent = moduleDir.getParentFile();
                while (parent != null) {
                    if (parent.getName() == "examples") {
                        return false;
                    }
                    parent = parent.getParentFile();
                }
                return true;
            }
            .filter { moduleDir ->
                if (compatibilityModules.contains(moduleDir.getName())) {
                    moduleDir.getParentFile().name == neo4jMajorVersion
                } else {
                    true
                }
            }
            .forEach { moduleDir ->
                def parent = moduleDir.getParentFile();
                def projectName = parent.name == "proc" ? ":proc-${moduleDir.name}" : ":${moduleDir.name}";
                include(projectName)
                project(projectName).projectDir = moduleDir
            }
}

registerSubModules(new File("./"))