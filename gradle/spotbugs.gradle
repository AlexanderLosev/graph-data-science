subprojects { proj ->

    plugins.withType(JavaLibraryPlugin) {
        proj.apply plugin: 'com.github.spotbugs'

        proj.spotbugs {
            toolVersion = spotbugsToolVersion
            effort = "max"

            // When  `ignoreFailures=false` the build will be failing on spotbugs errors
            ignoreFailures = true
            showStackTraces = false
            showProgress = true

            excludeFilter = file("$publicDir/etc/spotbugs/spotbugs-exclude.xml")
        }

        proj.tasks.spotbugsMain {
            def reportsDir = "${rootProject.buildDir}/reports/spotbugs/"
            def fileName = "${proj.name}-main"
            reports {
                html {
                    enabled = true
                    stylesheet = resources.text.fromFile("$publicDir/etc/spotbugs/xsl/fancy-hist.xsl")
                    destination = file("${reportsDir}/html/${fileName}.html")
                }
                xml {
                    enabled = true
                    destination = file("${reportsDir}/xml/${fileName}.xml")
                }
            }
        }

        proj.tasks.spotbugsTest {
            def reportsDir = "${rootProject.buildDir}/reports/spotbugs/"
            def fileName = "${proj.name}-test"
            reports {
                html {
                    enabled = true
                    stylesheet = resources.text.fromFile("$publicDir/etc/spotbugs/xsl/fancy-hist.xsl")
                    destination = file("${reportsDir}/html/${fileName}.html")
                }
                xml {
                    enabled = true
                    destination = file("${reportsDir}/xml/${fileName}.xml")
                }
            }
        }

    }
}
