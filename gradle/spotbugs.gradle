allprojects { proj ->

    plugins.withType(JavaLibraryPlugin) {

        proj.apply plugin: 'com.github.spotbugs'

        proj.spotbugs {
            toolVersion = spotbugsToolVersion
            effort = "max"

            // When  `ignoreFailures=false` the build will be failing on spotbugs errors
            ignoreFailures = true
            showStackTraces = false
            showProgress = true

            excludeFilter = file("$publicDir/etc/spotbugs/spotbugs-exclude.xml")
        }

        proj.tasks.withType(SpotBugsTask) {
            tasks.check.dependsOn it

            def taskName = it.name;
            def fileName = "${proj.name}-${taskName}"
            it.reports {
                html {
                    // Due to a known issue with SpotBugs, HTML and XML reports cannot be generated both at the same time.
                    enabled = false
                    stylesheet = resources.text.fromFile("$publicDir/etc/spotbugs/xsl/fancy-hist.xsl")
                    destination = file("${spotbugsReportsDir}/html/${fileName}.html")
                }
                xml {
                    enabled = true
                    destination = file("${spotbugsReportsDir}/xml/${fileName}.xml")
                }
            }
        }
    }
}
