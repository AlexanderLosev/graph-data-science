description = 'Neo4j Graph Algorithms :: Procedures'

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(':annotations')
    compile project(':algo')
    annotationProcessor project(':proc-processor')
    annotationProcessor project(':annotations')
    annotationProcessor 'org.immutables:value:2.8.1'
    annotationProcessor 'org.immutables:builder:2.8.1'

    compileOnly 'org.immutables:value-annotations:2.8.1'
    compileOnly group: 'org.neo4j', name: 'neo4j', version: ver.'neo4j'

    testCompile project(':test-utils')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from(javadoc.destinationDir)
}

publishing {
    publications {
        algo(MavenPublication) {
            project.shadow.component(it)
        }
    }
}

shadowJar {
    baseName = "neo4j-graph-algorithms-product"
    classifier = "standalone"
    minimize()
    exclude 'META-INF/versions/**/*'
    from licenseFile
    from generateLicensesFiles
}

task shadowCopy(type: Copy) {
    from(shadowJar)
    into distributionDir
}

task distZip(type: Zip) {
    description 'Create a ZIP archive of the main distribution JAR.'
    baseName = shadowJar.baseName
    classifier = shadowJar.classifier
    from shadowJar
    destinationDir file(distributionDir)
}
