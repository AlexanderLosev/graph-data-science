apply plugin: 'java-library'
apply plugin: 'nebula.info-broker'
apply plugin: 'nebula.info-props'
apply plugin: 'nebula.info-basic'
apply plugin: 'nebula.info-java'
apply plugin: 'nebula.info-scm'

description = 'Neo4j Graph Data Science :: Procedures'
dependencies {
    annotationProcessor project(':annotations')

    annotationProcessor group: 'org.immutables',           name: 'builder',            version: ver.'immutables'
    annotationProcessor group: 'org.immutables',           name: 'value',              version: ver.'immutables'

    api project(':proc-common')
    api project(':alpha-proc')
    api project(':proc-similarity')
    api project(':proc-centrality')
    api project(':proc-community')
    api project(':proc-beta')
    api project(':proc-catalog')

    implementation(
        project(':neo4j-adapter')
    )

    compileOnly group: 'org.immutables', name: 'value-annotations', version: ver.'immutables'
    compileOnly group: 'org.jetbrains',  name: 'annotations',       version: ver.'jetbrains-annotations'
    compileOnly group: 'org.neo4j',      name: 'neo4j',             version: ver.'neo4j'

    testAnnotationProcessor project(':annotations')

    testImplementation project(':proc-test')
    testImplementation project(':alpha-embeddings')
    testCompileOnly group: 'org.immutables', name: 'value-annotations', version: ver.'immutables'
    testCompileOnly group: 'org.immutables', name: 'builder',           version: ver.'immutables'
    testCompileOnly group: 'org.jetbrains',  name: 'annotations',       version: ver.'jetbrains-annotations'
}

// configure the keys that will be written to the info.properties file
infoBroker {
    includedManifestProperties = [
        'Build-Date',
        'Build-Java-Version',
        'Created-By',
        'Full-Change',
        'Implementation-Version',
        'X-Compile-Target-JDK',
    ]
}

// write the info.properties file on classes
task('processVersionResources', type: Copy, dependsOn: processResources) {
    into project.sourceSets.main.output.resourcesDir
    from(writeManifestProperties) {
        rename { "info.properties" }
        into "META-INF"
    }
}
classes.dependsOn(processVersionResources)
